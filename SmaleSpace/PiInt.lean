/-
Copyright (c) 2025 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.Algebra.MetricSpace.Lipschitz
import Mathlib.Topology.MetricSpace.HausdorffDistance

/-!
# Topological study of spaces `Œ† (n : ‚Ñ§), E n`

This is a copy of p√¢rt of the mathlib file `PiNat.lean`, with indexing by `‚Ñ§` instead of `‚Ñï`.

When `E n` are topological spaces, the space `Œ† (n : ‚Ñ§), E n` is naturally a topological space
(with the product topology). When `E n` are uniform spaces, it also inherits a uniform structure.
However, it does not inherit a canonical metric space structure of the `E n`. Nevertheless, one
can put a noncanonical metric space structure (or rather, several of them). This is done in this
file.

## Main definitions and results

One can define a combinatorial distance on `Œ† (n : ‚Ñ§), E n`, as follows:

* `PiInt.cylinder x n` is the set of points `y` with `x i = y i` for `-n < i < n`.
* `PiInt.firstDiff x y` is the first index at which `x i ‚â† y i` or `x (-i) ‚â† y (-i)`.
* `PiInt.dist x y` is equal to `(1/2) ^ (firstDiff x y)`. It defines a distance
  on `Œ† (n : ‚Ñï), E n`, compatible with the topology when the `E n` have the discrete topology.
* `PiInt.metricSpace`: the metric space structure, given by this distance. Not registered as an
  instance. This space is a complete metric space.
* `PiInt.metricSpaceOfDiscreteUniformity`: the same metric space structure, but adjusting the
  uniformity defeqness when the `E n` already have the discrete uniformity. Not registered as an
  instance
-/

noncomputable section

open Topology TopologicalSpace Set Metric Filter Function

attribute [local simp] pow_le_pow_iff_right‚ÇÄ one_lt_two inv_le_inv‚ÇÄ zero_le_two zero_lt_two

variable {E : ‚Ñ§ ‚Üí Type*}

namespace PiInt

/-! ### The firstDiff function -/

open Classical in
/-- In a product space `Œ† n, E n`, then `firstDiff x y` is the first index at which `x` and `y`
differ. If `x = y`, then by convention we set `firstDiff x x = 0`. -/
irreducible_def firstDiff (x y : Œ† n, E n) : ‚Ñï :=
  if h : ‚àÉ (i : ‚Ñï), x i ‚â† y i ‚à® x (-i) ‚â† y (-i) then Nat.find h else 0

theorem apply_firstDiff_ne_or {x y : Œ† n, E n} (h : x ‚â† y) :
    x (firstDiff x y) ‚â† y (firstDiff x y) ‚à® x (-firstDiff x y) ‚â† y (-firstDiff x y) := by
  have h' : ‚àÉ (i : ‚Ñï), x i ‚â† y i ‚à® x (-i) ‚â† y (-i) := by
    contrapose! h
    ext j
    rcases Int.eq_nat_or_neg j with ‚ü®w, rfl | rfl‚ü© <;> grind
  rw [firstDiff_def, dif_pos h']
  classical
  apply Nat.find_spec h'

theorem apply_nat_eq_of_lt_firstDiff {x y : Œ† n, E n} {n : ‚Ñï} (hn : n < firstDiff x y) :
    x n = y n := by
  classical
  rw [firstDiff_def] at hn
  split_ifs at hn with h
  ¬∑ have := Nat.find_min h hn
    grind
  ¬∑ exact (not_lt_zero' hn).elim

theorem apply_neg_eq_of_lt_firstDiff {x y : Œ† n, E n} {n : ‚Ñï} (hn : n < firstDiff x y) :
    x (-n) = y (-n) := by
  classical
  rw [firstDiff_def] at hn
  split_ifs at hn with h
  ¬∑ have := Nat.find_min h hn
    grind
  ¬∑ exact (not_lt_zero' hn).elim

theorem apply_eq {x y : Œ† n, E n} {i : ‚Ñ§} (h : (-firstDiff x y : ‚Ñ§) < i) (h' : i < firstDiff x y) :
    x i = y i := by
  rcases Int.eq_nat_or_neg i with ‚ü®w, rfl | rfl‚ü©
  ¬∑ apply apply_nat_eq_of_lt_firstDiff (by grind)
  ¬∑ apply apply_neg_eq_of_lt_firstDiff (by grind)

theorem firstDiff_comm (x y : Œ† n, E n) : firstDiff x y = firstDiff y x := by
  classical
  simp only [firstDiff_def, ne_comm]

theorem min_firstDiff_le (x y z : Œ† n, E n) (h : x ‚â† z) :
    min (firstDiff x y) (firstDiff y z) ‚â§ firstDiff x z := by
  by_contra! H
  rw [lt_min_iff] at H
  rcases apply_firstDiff_ne_or h with h' | h'
  ¬∑ apply h'
    calc
    x (firstDiff x z) = y (firstDiff x z) := apply_nat_eq_of_lt_firstDiff H.1
    _ = z (firstDiff x z) := apply_nat_eq_of_lt_firstDiff H.2
  ¬∑ apply h'
    calc
    x (-firstDiff x z) = y (-firstDiff x z) := apply_neg_eq_of_lt_firstDiff H.1
    _ = z (-firstDiff x z) := apply_neg_eq_of_lt_firstDiff H.2

/-! ### Cylinders -/

/-- In a product space `Œ† n, E n`, the cylinder set of length `n` around `x`, denoted
`cylinder x n`, is the set of sequences `y` that coincide with `x` on the first `n` symbols, i.e.,
such that `y i = x i` for all `i < n`.
-/
def cylinder (x : Œ† n, E n) (n : ‚Ñï) : Set (Œ† n, E n) :=
  { y | ‚àÄ i, (-n : ‚Ñ§) < i ‚Üí i < n ‚Üí y i = x i }

theorem cylinder_eq_pi (x : Œ† n, E n) (n : ‚Ñï) :
    cylinder x n = Set.pi (Finset.Ioo (-n : ‚Ñ§) n : Set ‚Ñ§) fun i : ‚Ñ§ => {x i} := by
  ext y
  simp [cylinder]

@[simp]
theorem cylinder_zero (x : Œ† n, E n) : cylinder x 0 = univ := by
  simp [cylinder_eq_pi]

theorem cylinder_anti (x : Œ† n, E n) {m n : ‚Ñï} (h : m ‚â§ n) : cylinder x n ‚äÜ cylinder x m :=
  fun _y hy i hi h'i ‚Ü¶ hy i (by grind) (by grind)

@[simp]
theorem mem_cylinder_iff {x y : Œ† n, E n} {n : ‚Ñï} :
    y ‚àà cylinder x n ‚Üî ‚àÄ i, (-n : ‚Ñ§) < i ‚Üí i < n ‚Üí y i = x i :=
  Iff.rfl

theorem self_mem_cylinder (x : Œ† n, E n) (n : ‚Ñï) : x ‚àà cylinder x n := by simp

theorem mem_cylinder_iff_eq {x y : Œ† n, E n} {n : ‚Ñï} :
    y ‚àà cylinder x n ‚Üî cylinder y n = cylinder x n := by
  constructor
  ¬∑ intro hy
    apply Subset.antisymm
    ¬∑ intro z hz i hi h'i
      rw [‚Üê hy i hi h'i]
      exact hz i hi h'i
    ¬∑ intro z hz i hi h'i
      rw [hy i hi h'i]
      exact hz i hi h'i
  ¬∑ intro h
    rw [‚Üê h]
    exact self_mem_cylinder _ _

theorem mem_cylinder_comm (x y : Œ† n, E n) (n : ‚Ñï) : y ‚àà cylinder x n ‚Üî x ‚àà cylinder y n := by
  simp [eq_comm]

theorem mem_cylinder_iff_le_firstDiff {x y : Œ† n, E n} (hne : x ‚â† y) (i : ‚Ñï) :
    x ‚àà cylinder y i ‚Üî i ‚â§ firstDiff x y := by
  constructor
  ¬∑ intro h
    by_contra!
    rcases apply_firstDiff_ne_or hne with h' | h' <;>
    apply h' (h _ (by grind) (by grind))
  ¬∑ intro hi j hj h'j
    exact apply_eq (by grind) (by grind)

theorem mem_cylinder_firstDiff (x y : Œ† n, E n) : x ‚àà cylinder y (firstDiff x y) := fun _i hi h'i =>
  apply_eq hi h'i

theorem cylinder_eq_cylinder_of_le_firstDiff (x y : Œ† n, E n) {n : ‚Ñï} (hn : n ‚â§ firstDiff x y) :
    cylinder x n = cylinder y n := by
  rw [‚Üê mem_cylinder_iff_eq]
  intro i hi h'i
  exact apply_eq (by grind) (by grind)

theorem update_mem_cylinder (x : Œ† n, E n) (n : ‚Ñï) (y : E n) : update x n y ‚àà cylinder x n :=
  mem_cylinder_iff.2 fun i hi h'i => by simp [h'i.ne]

/-!
### A distance function on `Œ† n, E n`

We define a distance function on `Œ† n, E n`, given by `dist x y = (1/2)^n` where `n` is the first
index at which `x` and `y` differ. When each `E n` has the discrete topology, this distance will
define the right topology on the product space. We do not record a global `Dist` instance nor
a `MetricSpace` instance, as other distances may be used on these spaces, but we register them as
local instances in this section.
-/

open Classical in
/-- The distance function on a product space `Œ† n, E n`, given by `dist x y = (1/2)^n` where `n` is
the first index at which `x` and `y` differ. -/
protected def dist : Dist (Œ† n, E n) :=
  ‚ü®fun x y => if x ‚â† y then (2‚Åª¬π : ‚Ñù) ^ firstDiff x y else 0‚ü©

attribute [local instance] PiInt.dist

theorem dist_eq_of_ne {x y : Œ† n, E n} (h : x ‚â† y) : dist x y = (2‚Åª¬π : ‚Ñù) ^ firstDiff x y := by
  simp [dist, h]

protected theorem dist_self (x : Œ† n, E n) : dist x x = 0 := by simp [dist]

protected theorem dist_comm (x y : Œ† n, E n) : dist x y = dist y x := by
  classical
  simp [dist, @eq_comm _ x y, firstDiff_comm]

protected theorem dist_nonneg (x y : Œ† n, E n) : 0 ‚â§ dist x y := by
  rcases eq_or_ne x y with (rfl | h)
  ¬∑ simp [dist]
  ¬∑ simp [dist, h]

theorem dist_triangle_nonarch (x y z : Œ† n, E n) : dist x z ‚â§ max (dist x y) (dist y z) := by
  rcases eq_or_ne x z with (rfl | hxz)
  ¬∑ simp [PiInt.dist_self x, PiInt.dist_nonneg]
  rcases eq_or_ne x y with (rfl | hxy)
  ¬∑ simp
  rcases eq_or_ne y z with (rfl | hyz)
  ¬∑ simp
  simp only [dist_eq_of_ne, hxz, hxy, hyz, inv_le_inv‚ÇÄ, inv_pow, zero_lt_two, Ne,
    not_false_iff, le_max_iff, pow_le_pow_iff_right‚ÇÄ, one_lt_two, pow_pos,
    min_le_iff.1 (min_firstDiff_le x y z hxz)]

protected theorem dist_triangle (x y z : Œ† n, E n) : dist x z ‚â§ dist x y + dist y z :=
  calc
    dist x z ‚â§ max (dist x y) (dist y z) := dist_triangle_nonarch x y z
    _ ‚â§ dist x y + dist y z := max_le_add_of_nonneg (PiInt.dist_nonneg _ _) (PiInt.dist_nonneg _ _)

protected theorem eq_of_dist_eq_zero (x y : Œ† n, E n) (hxy : dist x y = 0) : x = y := by
  rcases eq_or_ne x y with (rfl | h); ¬∑ rfl
  simp [dist_eq_of_ne h] at hxy

theorem mem_cylinder_iff_dist_le {x y : Œ† n, E n} {n : ‚Ñï} :
    y ‚àà cylinder x n ‚Üî dist y x ‚â§ 2‚Åª¬π ^ n := by
  rcases eq_or_ne y x with (rfl | hne)
  ¬∑ simp [PiInt.dist_self]
  suffices (‚àÄ i : ‚Ñ§, (-n : ‚Ñ§) < i ‚Üí i < n ‚Üí y i = x i) ‚Üî n ‚â§ firstDiff y x by
    simpa [dist_eq_of_ne hne]
  constructor
  ¬∑ intro hy
    by_contra! H
    rcases apply_firstDiff_ne_or hne with h' | h' <;>
    exact h' (hy _ (by grind) (by grind))
  ¬∑ intro h i hi h'i
    exact apply_eq (by grind) (by grind)

theorem mem_cylinder_succ_iff_dist_lt {x y : Œ† n, E n} {n : ‚Ñï} :
    y ‚àà cylinder x (n + 1) ‚Üî dist y x < 2‚Åª¬π ^ n := by
  rcases eq_or_ne y x with (rfl | hne)
  ¬∑ simp [PiInt.dist_self]
  suffices (‚àÄ i : ‚Ñ§, (-(n + 1) : ‚Ñ§) < i ‚Üí i < n + 1 ‚Üí y i = x i) ‚Üî n < firstDiff y x by
    simp only [neg_add_rev, Int.reduceNeg, add_neg_lt_iff_lt_add, mem_cylinder_iff, Nat.cast_add,
      Nat.cast_one, dist_eq_of_ne hne, inv_pow] at this ‚ä¢
    simp only [this]
    rw [inv_lt_inv‚ÇÄ (by positivity) (by positivity), pow_lt_pow_iff_right‚ÇÄ one_lt_two]
  constructor
  ¬∑ intro hy
    by_contra! H
    rcases apply_firstDiff_ne_or hne with h' | h' <;>
    exact h' (hy _ (by grind) (by grind))
  ¬∑ intro h i hi h'i
    exact apply_eq (by grind) (by grind)

theorem apply_eq_of_dist_lt {x y : Œ† n, E n} {n : ‚Ñï} (h : dist x y < 2‚Åª¬π ^ n) {i : ‚Ñ§}
    (hi : (-n : ‚Ñ§) ‚â§ i) (h'i : i ‚â§ n) : x i = y i :=
  mem_cylinder_succ_iff_dist_lt.2 h i (by grind) (by grind)

lemma dist_le_iff {x y : Œ† n, E n} {s : ‚Ñù} (hs : 0 ‚â§ s) :
    (dist x y ‚â§ s) ‚Üî (‚àÄ (n : ‚Ñï), s < (2 ‚Åª¬π) ^ n ‚Üí dist x y < 2‚Åª¬π ^ n) := by
  refine ‚ü®fun h n hs ‚Ü¶ by order, fun h ‚Ü¶ ?_‚ü©
  rcases eq_or_ne x y with rfl | hxy
  ¬∑ simp [hs, PiInt.dist_self]
  rw [dist_eq_of_ne hxy] at h ‚ä¢
  contrapose! h
  exact ‚ü®firstDiff x y, h, le_rfl‚ü©

/-- A function to a pseudo-metric-space is `1`-Lipschitz if and only if points in the same cylinder
of length `n` are sent to points within distance `(1/2)^n`.
Not expressed using `LipschitzWith` as we don't have a metric space structure -/
theorem lipschitz_with_one_iff_forall_dist_image_le_of_mem_cylinder {Œ± : Type*}
    [PseudoMetricSpace Œ±] {f : (Œ† n, E n) ‚Üí Œ±} :
    (‚àÄ x y : Œ† n, E n, dist (f x) (f y) ‚â§ dist x y) ‚Üî
      ‚àÄ x y n, y ‚àà cylinder x n ‚Üí dist (f x) (f y) ‚â§ 2‚Åª¬π ^ n := by
  constructor
  ¬∑ intro H x y n hxy
    apply (H x y).trans
    rw [PiInt.dist_comm]
    exact mem_cylinder_iff_dist_le.1 hxy
  ¬∑ intro H x y
    rcases eq_or_ne x y with (rfl | hne)
    ¬∑ simp [PiInt.dist_nonneg]
    rw [dist_eq_of_ne hne]
    apply H x y (firstDiff x y)
    rw [firstDiff_comm]
    exact mem_cylinder_firstDiff _ _

variable (E)
variable [‚àÄ n, TopologicalSpace (E n)] [‚àÄ n, DiscreteTopology (E n)]

theorem isOpen_cylinder (x : Œ† n, E n) (n : ‚Ñï) : IsOpen (cylinder x n) := by
  rw [PiInt.cylinder_eq_pi]
  exact isOpen_set_pi (Finset.finite_toSet _) fun a _ => isOpen_discrete _

theorem isTopologicalBasis_cylinders :
    IsTopologicalBasis {s : Set (Œ† n, E n) | ‚àÉ (x : Œ† n, E n) (n : ‚Ñï), s = cylinder x n} := by
  apply isTopologicalBasis_of_isOpen_of_nhds
  ¬∑ rintro u ‚ü®x, n, rfl‚ü©
    apply isOpen_cylinder
  ¬∑ intro x u hx u_open
    obtain ‚ü®v, ‚ü®U, F, -, rfl‚ü©, xU, Uu‚ü© :
        ‚àÉ v ‚àà { S : Set (‚àÄ i : ‚Ñ§, E i) | ‚àÉ (U : ‚àÄ i : ‚Ñ§, Set (E i)) (F : Finset ‚Ñ§),
          (‚àÄ i : ‚Ñ§, i ‚àà F ‚Üí U i ‚àà { s : Set (E i) | IsOpen s }) ‚àß S = (F : Set ‚Ñ§).pi U },
        x ‚àà v ‚àß v ‚äÜ u :=
      (isTopologicalBasis_pi fun n : ‚Ñ§ => isTopologicalBasis_opens).exists_subset_of_mem_open hx
        u_open
    rcases Finset.bddAbove (F.image Int.natAbs) with ‚ü®n, hn‚ü©
    refine ‚ü®cylinder x (n + 1), ‚ü®x, n + 1, rfl‚ü©, self_mem_cylinder _ _, Subset.trans ?_ Uu‚ü©
    intro y hy
    suffices ‚àÄ i : ‚Ñ§, i ‚àà F ‚Üí y i ‚àà U i by simpa
    intro i hi
    have : y i = x i := by
      have := hn (Finset.mem_image_of_mem Int.natAbs hi)
      apply mem_cylinder_iff.1 hy i (by grind) (by grind)
    rw [this]
    simp only [Set.mem_pi, Finset.mem_coe] at xU
    exact xU i hi

variable {E}

theorem isOpen_iff_dist (s : Set (Œ† n, E n)) :
    IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ‚àÄ y, dist x y < Œµ ‚Üí y ‚àà s := by
  constructor
  ¬∑ intro hs x hx
    obtain ‚ü®v, ‚ü®y, n, rfl‚ü©, h'x, h's‚ü© :
        ‚àÉ v ‚àà { s | ‚àÉ (x : ‚àÄ n : ‚Ñ§, E n) (n : ‚Ñï), s = cylinder x n }, x ‚àà v ‚àß v ‚äÜ s :=
      (isTopologicalBasis_cylinders E).exists_subset_of_mem_open hx hs
    rw [‚Üê mem_cylinder_iff_eq.1 h'x] at h's
    refine
      ‚ü®(2‚Åª¬π : ‚Ñù) ^ n, by simp, fun y hy => h's
        fun i hi h'i => (apply_eq_of_dist_lt hy hi.le h'i.le).symm‚ü©
  ¬∑ intro h
    refine (isTopologicalBasis_cylinders E).isOpen_iff.2 fun x hx => ?_
    rcases h x hx with ‚ü®Œµ, Œµpos, hŒµ‚ü©
    obtain ‚ü®n, hn‚ü© : ‚àÉ n : ‚Ñï, (2‚Åª¬π : ‚Ñù) ^ n < Œµ := exists_pow_lt_of_lt_one Œµpos (by norm_num)
    refine ‚ü®cylinder x n, ‚ü®x, n, rfl‚ü©, self_mem_cylinder x n, fun y hy => hŒµ y ?_‚ü©
    rw [PiInt.dist_comm]
    exact (mem_cylinder_iff_dist_le.1 hy).trans_lt hn

/-- Metric space structure on `Œ† (n : ‚Ñ§), E n` when the spaces `E n` have the discrete topology,
where the distance is given by `dist x y = (1/2)^n`, where `n` is the smallest index where `x` and
`y` differ. Not registered as a global instance by default.
Warning: this definition makes sure that the topology is defeq to the original product topology,
but it does not take care of a possible uniformity. If the `E n` have a uniform structure, then
there will be two non-defeq uniform structures on `Œ† n, E n`, the product one and the one coming
from the metric structure. In this case, use `metricSpaceOfDiscreteUniformity` instead. -/
protected def metricSpace : MetricSpace (Œ† n, E n) :=
  MetricSpace.ofDistTopology dist PiInt.dist_self PiInt.dist_comm PiInt.dist_triangle
    isOpen_iff_dist PiInt.eq_of_dist_eq_zero

/-- Metric space structure on `Œ† (n : ‚Ñ§), E n` when the spaces `E n` have the discrete uniformity,
where the distance is given by `dist x y = (1/2)^n`, where `n` is the smallest index where `x` and
`y` differ. Not registered as a global instance by default. -/
protected def metricSpaceOfDiscreteUniformity {E : ‚Ñ§ ‚Üí Type*} [‚àÄ n, UniformSpace (E n)]
    (h : ‚àÄ n, uniformity (E n) = ùìü SetRel.id) : MetricSpace (Œ† n, E n) :=
  haveI : ‚àÄ n, DiscreteTopology (E n) := fun n => discreteTopology_of_discrete_uniformity (h n)
  { dist_triangle := PiInt.dist_triangle
    dist_comm := PiInt.dist_comm
    dist_self := PiInt.dist_self
    eq_of_dist_eq_zero := PiInt.eq_of_dist_eq_zero _ _
    toUniformSpace := Pi.uniformSpace _
    uniformity_dist := by
      simp only [Pi.uniformity, h, SetRel.id, comap_principal, preimage_setOf_eq]
      apply le_antisymm
      ¬∑ simp only [le_iInf_iff, le_principal_iff]
        intro Œµ Œµpos
        obtain ‚ü®n, hn‚ü© : ‚àÉ n, (2‚Åª¬π : ‚Ñù) ^ n < Œµ := exists_pow_lt_of_lt_one Œµpos (by norm_num)
        apply
          @mem_iInf_of_iInter _ _ _ _ _ (Finset.Ioo (-n : ‚Ñ§) n).finite_toSet fun i =>
            { p : (Œ† n : ‚Ñ§, E n) √ó (Œ† n : ‚Ñ§, E n) | p.fst i = p.snd i }
        ¬∑ simp only [mem_principal, setOf_subset_setOf, imp_self, imp_true_iff]
        ¬∑ rintro ‚ü®x, y‚ü© hxy
          simp only [SetLike.coe_sort_coe, mem_iInter, mem_setOf_eq, Subtype.forall, Finset.mem_Ioo,
            and_imp] at hxy
          apply lt_of_le_of_lt _ hn
          rw [‚Üê mem_cylinder_iff_dist_le, mem_cylinder_iff]
          exact hxy
      ¬∑ simp only [le_iInf_iff, le_principal_iff]
        intro n
        refine mem_iInf_of_mem (2‚Åª¬π ^ n.natAbs : ‚Ñù) ?_
        refine mem_iInf_of_mem (by positivity) ?_
        simp only [mem_principal, setOf_subset_setOf, Prod.forall]
        intro x y hxy
        exact apply_eq_of_dist_lt hxy (by grind) (by grind) }

/- Not suitable for mathlib, but good for the project -/
attribute [instance] PiInt.metricSpace

protected theorem completeSpace : CompleteSpace (Œ† n, E n) := by
  refine Metric.complete_of_convergent_controlled_sequences (fun n => 2‚Åª¬π ^ n) (by simp) ?_
  intro u hu
  refine ‚ü®fun n => u n.natAbs n, tendsto_pi_nhds.2 fun i => ?_‚ü©
  refine tendsto_const_nhds.congr' ?_
  filter_upwards [Filter.Ici_mem_atTop i.natAbs] with n hn
  apply apply_eq_of_dist_lt (hu i.natAbs _ n le_rfl hn) (by grind) (by grind)

end PiInt
