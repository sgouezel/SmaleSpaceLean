import SmaleSpace.LocMaximal

open scoped Uniformity Topology
open Function Set Filter Metric SetRel

variable {X : Type*} [MetricSpace X] {T' : X ‚Üí X} {T : X ‚âÉ X} {A B : Set X}
  {U V : Set (X √ó X)} {a b c o s u x y z : X} {Œµ Œµ' Œ¥ : ‚Ñù} {n : ‚Ñï}
  [CompleteSpace X]

namespace IsExtLocallyMaxHyperbolicSet

variable {A : Set X} (hT : IsExtLocallyMaxHyperbolicSet T A)
include hT

local notation3 "Œ¥‚ÇÄ" => hT.deltaZero
local notation3 "C‚ÇÄ" => hT.C0
local notation3 "œÅ" => hT.rho
local notation3 "‚ÅÖ" x ", " y "‚ÅÜ" => hT.bracket x y
local notation3 "Œ¥‚ÇÅ" => hT.reduceScale Œ¥‚ÇÄ
local notation3 "Œ¥‚ÇÇ" => hT.reduceScale Œ¥‚ÇÅ

/-- Let `Œ¥ > 0`. Let `Œµ` be small enough compared to `Œ¥`. Then any `Œµ`-pseudo-orbit in the future
can be `4Œ¥`-shadowed by a genuine orbit, starting from the `Œ¥`-unstable manifold of the initial
point.

We give the conditions on `Œµ` in explicit form, to make it possible to check them uniformly
over families of maps. First, we fix `M` large enough so that `2 * œÅ ^ M * Œ¥ ‚â§ hT.reduceScale Œ¥`.
Then, `Œµ` should be small enough that an `Œµ`-pseudo-orbit does not deviate from a genuine orbit
by more than `hT.reduceScale Œ¥ / 2` until time `M`.
-/
lemma future_shadowing_precise
    (hŒ¥ : 0 < Œ¥) (h''Œ¥ : Œ¥ ‚â§ Œ¥‚ÇÄ / 2) (x : ‚Ñï ‚Üí X)
    (hx : ‚àÄ n, dist (T (x n)) (x (n + 1)) ‚â§ Œµ) (h'x : ‚àÄ n, x n ‚àà A)
    {M : ‚Ñï} (hM : 2 * C‚ÇÄ * œÅ ^ M * Œ¥ ‚â§ hT.reduceScale Œ¥)
    (hŒµ : ‚àÄ (u : ‚Ñï ‚Üí X), (‚àÄ n, dist (T (u n)) (u (n + 1)) ‚â§ Œµ) ‚Üí
      ‚àÄ i ‚â§ M, dist (T^[i] (u 0)) (u i) ‚â§ hT.reduceScale Œ¥ / 2) :
    ‚àÉ p ‚àà locUnstable T Œ¥ (x 0) ‚à© A, ‚àÄ n, dist (x n) (T ^[n] p) ‚â§ 4 * Œ¥ := by
  -- Start by recording useful basic facts
  have : Nonempty X := ‚ü®x 0‚ü©
  have := hT.one_le_C0
  have : 0 ‚â§ C‚ÇÄ := by linarith
  have := hT.rho_pos
  have := hT.continuous
  have rhoM : C‚ÇÄ * œÅ ^ M ‚â§ 2‚Åª¬π := by
    have W := hM.trans hT.reduceScale_le_half_self
    field_simp at W
    linarith
  have : M ‚â† 0 := by
    intro h
    simp only [h, pow_zero] at rhoM
    norm_num at rhoM
    linarith
  have h'Œ¥ : Œ¥ ‚â§ Œ¥‚ÇÄ := by linarith [hT.deltaZero_pos]
  have L n : Function.LeftInverse T.symm^[n] T^[n] := (Equiv.leftInverse_symm T).iterate _
  have L' n : Function.LeftInverse T^[n] T.symm^[n] := (Equiv.leftInverse_symm T.symm).iterate _
  /- define inductively a sequence by `y‚ÇÄ = x‚ÇÄ`, and `y_{n+1}` the intersection of `W^u (T^M y‚Çô)`
  and `W^s (x_{M * (n + 1)})`.
  We will prove in the course of the proof that the displacement along the unstable manifold made
  at step `n` when going from `T^M y‚Çô` to `y_{n+1}` is always by at most `Œ¥`. Pulling back `y‚Çô` by
  `T^{-n}` inside `W^u (x‚ÇÄ)`, one gets a sequence with jumps bounded by `Œ¥ 2^{-n}`, therefore Cauchy
  and converging. By design, its limit will be the desired point.
  -/
  let y := Nat.rec (motive := fun n ‚Ü¶ X) (x 0) (fun n q ‚Ü¶ ‚ÅÖT^[M] q, x (M * (n + 1))‚ÅÜ)
  /- First, we will check inductively that `y‚Çô` is on the `Œ¥`-stable manifold of `x_{M * n}`.
  It follows that its image under `T^M` is even closer to `x_{M * (n + 1)}`, therefore taking the
  bracket one remains within `Œ¥`, completing the induction. -/
  have A_aux n (hn : y n ‚àà locStable T Œ¥ (x (M * n))) :
      dist (x (M * (n + 1))) (T^[M] (y n)) ‚â§ hT.reduceScale Œ¥ := by
    apply (dist_triangle_left _ _ (T^[M] (x (M * n)))).trans
    have : dist (T^[M] (x (M * n))) (x (M * (n + 1))) ‚â§ hT.reduceScale Œ¥ / 2 :=
      hŒµ (fun k ‚Ü¶ x (M * n + k)) (fun k ‚Ü¶ hx (M * n + k)) M le_rfl
    have : dist (T^[M] (x (M * n))) (T^[M] (y n)) ‚â§ C‚ÇÄ * œÅ ^ M * Œ¥ :=
      hT.dist_iterate_le_mul_of_mem_locStable (h'x _) h'Œ¥ hn M
    linarith
  have B n : y n ‚àà locStable T Œ¥ (x (M * n)) ‚à© A := by
    induction n with
    | zero => simp [y, hŒ¥.le, locStable, h'x 0]
    | succ n ih =>
      rw [show y (n + 1) = ‚ÅÖT^[M] (y n), x (M * (n + 1))‚ÅÜ by rfl]
      refine ‚ü®?_, hT.bracket_mem (hT.mapsTo.iterate _ ih.2) (h'x _)‚ü©
      apply hT.bracket_mem_locStable (hT.mapsTo.iterate _ ih.2) (h'x _) _ h'Œ¥
      rw [dist_comm]
      apply A_aux _ ih.1
  have A' n : dist (x (M * (n + 1))) (T^[M] (y n)) ‚â§ hT.reduceScale Œ¥ :=
    A_aux n (B n).1
  have C n : y (n + 1) ‚àà locUnstable T Œ¥ (T^[M] (y n)) := by
    rw [show y (n + 1) = ‚ÅÖT^[M] (y n), x (M * (n + 1))‚ÅÜ by rfl]
    apply hT.bracket_mem_locUnstable (hT.mapsTo.iterate _ (B n).2) (h'x _) _ h'Œ¥
    rw [dist_comm]
    exact A' n
  /- Define a sequence `z_{i, n}` around `x_{M * i}`, as the pullback of `y_{i + n}` from `n` times
  later in the future. We are mostly interested in `z_{0,n}` (which will converge to the desired
  point) but for the estimates we will need to control what happens at an arbitrary `i`. Thanks to
  the contracting properties of `T‚Åª¬π` along unstable manifolds, `n ‚Ü¶ z_{i,n}` has successive jumps
  of size at most `2^{-(n+1)} Œ¥`, and is therefore converging to a limit `p·µ¢` belonging to
  the stable manifold of `y·µ¢` of size `Œ¥`. -/
  let z i n := T.symm^[M * n] (y (i + n))
  have Z i n : z i (n + 1) ‚àà locUnstable T (C‚ÇÄ * œÅ ^ (M * (n + 1)) * Œ¥) (z i n) := by
    convert hT.iterate_symm_mem_locUnstable_mul (hT.mapsTo.iterate _ (B _).2) h'Œ¥ (C (i + n))
      (n := M * (n + 1)) using 2
    rw [mul_add, iterate_add_apply, mul_one, L]
  have Z' i n : z i (n + 1) ‚àà locUnstable T (2‚Åª¬π ^ (n + 1) * Œ¥) (z i n) := by
    apply locUnstable_mono _ (Z i n)
    rw [pow_mul]
    gcongr
    calc C‚ÇÄ * (œÅ ^ M) ^ (n + 1)
    _ ‚â§ C‚ÇÄ ^ (n + 1) * (œÅ ^ M) ^ (n + 1) := by
      gcongr; exact le_self_pow‚ÇÄ hT.one_le_C0 (by grind)
    _ = (C‚ÇÄ * œÅ ^ M) ^ (n + 1) := by ring
    _ ‚â§ 2‚Åª¬π ^ (n + 1) := by gcongr
  let p i := limUnder atTop (z i)
  have Lim i : Tendsto (z i) atTop (ùìù (p i)) := by
    apply tendsto_nhds_limUnder (cauchySeq_tendsto_of_complete ?_)
    apply cauchySeq_of_le_geometric_two (C := Œ¥) (fun n ‚Ü¶ ?_)
    apply ((Z' i n).1 0).trans_eq
    simp only [inv_pow, pow_succ]
    field_simp
  have I n : T^[M * n] (p 0) = p n := by
    have L1 : Tendsto (fun i ‚Ü¶ T^[M * n] (z 0 i)) atTop (ùìù (T^[M * n] (p 0))) := by
      have : ContinuousAt (T^[M * n]) (p 0) := by fun_prop
      exact Tendsto.comp this (Lim 0)
    have L'1 : Tendsto (fun i ‚Ü¶ T^[M * n] (z 0 (n + i))) atTop (ùìù (T^[M * n] (p 0))) := by
      simpa [add_comm] using (tendsto_add_atTop_iff_nat n).2 L1
    have L2 : Tendsto (fun i ‚Ü¶ T^[M * n] (z 0 (n + i))) atTop (ùìù (p n)) := by
      convert Lim n using 2 with i
      simp only [z]
      rw [mul_add, iterate_add_apply, L', zero_add]
    exact tendsto_nhds_unique L'1 L2
  have H i n : z i n ‚àà locUnstable T ((1 - 2‚Åª¬π ^ n) * Œ¥) (y i) := by
    induction n with
    | zero => simp [z]
    | succ n ih =>
      have I : (1 - 2‚Åª¬π ^ n) * Œ¥ + 2‚Åª¬π ^ (n + 1) * Œ¥ = (1 - 2‚Åª¬π ^ (n + 1)) * Œ¥ := by ring
      have := mem_locUnstable_trans ih (Z' i n)
      rwa [I] at this
  have P i : p i ‚àà locUnstable T Œ¥ (y i) ‚à© A := by
    refine IsClosed.mem_of_tendsto ((hT.isClosed_locUnstable (B _).2 h'Œ¥).inter hT.isClosed) (Lim i)
      (Eventually.of_forall (fun n ‚Ü¶ ‚ü®?_, ?_‚ü©))
    ¬∑ apply locUnstable_mono _ (H i n)
      simp [sub_mul, hŒ¥.le]
    ¬∑ apply hT.mapsTo_symm.iterate
      exact (B _).2
  /- The point `p‚ÇÄ` will satisfy the conclusion of the lemma. To control the distance between
  `T^n (p‚ÇÄ)` and `x‚Çô`, we write `n = M a + b`. The points `x_{M a}` and `y‚Çê` are on the same
  stable manifold of size `Œ¥`, so their images under `T^b` are `Œ¥`-close. Moreover, the image
  `T^b (x_{M a})` is `Œ¥` close to `x_{M a + b}` by the `Œµ`-pseudoorbit property. It remains
  to see that `T^b y‚Çê` is `2Œ¥`-close to `T^n p‚ÇÄ` to conclude. At time `(M+1) a`, the corresponding
  points are on a common unstable manifold of size `2Œ¥` by construction (as `y_{a+1}` is on the
  `Œ¥`-unstable manifold of `T^M y‚Çê` and `p_{a+1}` is on the `Œ¥`-unstable manifold of `y_{a+1}`.)
  Pulling back by `T^{-(M-b)}`, which contracts distances along unstable manifolds, we get the
  desired bound by `2Œ¥`. -/
  refine ‚ü®p 0, P 0, fun n ‚Ü¶ ?_‚ü©
  obtain ‚ü®a, b, hb, rfl‚ü© : ‚àÉ (a b : ‚Ñï), b < M ‚àß n = M * a + b :=
    ‚ü®n / M, n % M, Nat.mod_lt n (by omega), by rw [Nat.div_add_mod]‚ü©
  have : dist (x (M * a + b)) (T^[M * a + b] (p 0)) ‚â§ dist (x (M * a + b)) (T^[b] (x (M * a)))
      + dist (T^[b] (x (M * a))) (T^[b] (y a)) + dist (T^[b] (y a)) (T^[M * a + b] (p 0)) :=
    dist_triangle4 _ _ _ _
  have : dist (x (M * a + b)) (T^[b] (x (M * a))) ‚â§ Œ¥ := by
    have : dist (x (M * a + b)) (T^[b] (x (M * a))) ‚â§ hT.reduceScale Œ¥ / 2 := by
      rw [dist_comm]
      exact hŒµ (fun i ‚Ü¶ x (M * a + i)) (fun n ‚Ü¶ hx _) b hb.le
    exact this.trans (by linarith [hT.reduceScale_le_half_self (Œµ := Œ¥)])
  have : dist (T^[b] (x (M * a))) (T^[b] (y a)) ‚â§ Œ¥ := (iterate_mem_locStable (B _).1 _).1 0
  have : dist (T^[b] (y a)) (T^[M * a + b] (p 0)) ‚â§ 2 * Œ¥ := by
    have E1 : T^[b] (y a) = T.symm^[M-b] (T^[M] (y a)) := by
      have : M = M-b + b := by omega
      nth_rw 2 [this]
      rw [iterate_add_apply, L]
    have E2 : T^[M * a + b] (p 0) = T.symm^[M-b] (p (a + 1)) := by
      have : M * (a + 1) = (M - b) + (M * a + b) := by rw [mul_add]; omega
      rw [‚Üê I (a + 1), this, iterate_add_apply _ (M - b), L]
    rw [E1, E2]
    have : p (a + 1) ‚àà locUnstable T (Œ¥ + Œ¥) (T^[M] (y a)) :=
      mem_locUnstable_trans (C a) (P (a + 1)).1
    apply ((iterate_symm_mem_locUnstable this (M - b)).1 0).trans_eq (by ring)
  linarith

/-- Consider a locally maximal hyperbolic set. Let `Œ¥ > 0`. Let `Œµ` be small enough compared
to `Œ¥`. Then any `Œµ`-pseudo-orbit can be `4Œ¥`-shadowed by a genuine orbit.

We give the conditions on `Œµ` in explicit form, to make it possible to check them uniformly
over families of maps. First, we fix `M` large enough so that `2 * œÅ ^ M * Œ¥ ‚â§ hT.reduceScale Œ¥`.
Then, `Œµ` should be small enough that an `Œµ`-pseudo-orbit does not deviate from a genuine orbit
by more than `hT.reduceScale Œ¥ / 2` until time `M`.
-/
lemma shadowing_precise
    (hŒ¥ : 0 < Œ¥) (h'Œ¥ : Œ¥ ‚â§ Œ¥‚ÇÇ / 8) (x : ‚Ñ§ ‚Üí X)
    (hx : ‚àÄ n, dist (T (x n)) (x (n + 1)) ‚â§ Œµ) (h'x : ‚àÄ n, x n ‚àà A)
    {M : ‚Ñï} (hM : 2 * C‚ÇÄ * œÅ ^ M * Œ¥ ‚â§ hT.reduceScale Œ¥)
    (hŒµ : ‚àÄ (u : ‚Ñï ‚Üí X), (‚àÄ n, dist (T (u n)) (u (n + 1)) ‚â§ Œµ) ‚Üí
      ‚àÄ i ‚â§ M, dist (T^[i] (u 0)) (u i) ‚â§ hT.reduceScale Œ¥ / 2) :
    ‚àÉ p ‚àà A, ‚àÄ (n : ‚Ñï), dist (x n) (T^[n] p) ‚â§ 4 * Œ¥ ‚àß dist (x (-n)) (T.symm^[n] p) ‚â§ 4 * Œ¥ := by
  have h'Œ¥ : Œ¥ ‚â§ Œ¥‚ÇÄ / 2 := by linarith [hT.deltaTwo_le_deltaOne, hT.deltaOne_le_deltaZero]
  have E n : ‚àÉ p ‚àà A, (‚àÄ (i : ‚Ñï), dist (x i) (T^[i] p) ‚â§ 4 * Œ¥)
      ‚àß (‚àÄ (i : ‚Ñï), i ‚â§ n ‚Üí dist (x (-i)) (T.symm^[i] p) ‚â§ 4 * Œ¥) := by
    rcases hT.future_shadowing_precise hŒ¥ h'Œ¥ (Œµ := Œµ) (fun i ‚Ü¶ x (i - n : ‚Ñ§))
      (fun i ‚Ü¶ by convert hx (i - n) using 3; omega) (fun i ‚Ü¶ h'x _) hM hŒµ with ‚ü®q, h'q, hq‚ü©
    refine ‚ü®T^[n] q, hT.mapsTo.iterate _ h'q.2, fun i ‚Ü¶ ?_, fun i hi ‚Ü¶ ?_‚ü©
    ¬∑ rw [‚Üê iterate_add_apply]
      convert hq (i + n)
      omega
    ¬∑ have L : Function.LeftInverse T.symm^[i] T^[i] := (Equiv.leftInverse_symm T).iterate _
      have : n = i + (n - i) := by omega
      rw [this, iterate_add_apply, L]
      convert hq (n - i) using 3
      omega
  choose p hpA hp h'p using E
  have B n : dist (p n) (p (n + 1)) ‚â§ C‚ÇÄ * œÅ ^ n * (2 * Œ¥‚ÇÅ) := by
    apply hT.expansive_finite_time (hpA _) (hpA _) (fun i hi ‚Ü¶ ?_) (fun i hi ‚Ü¶ ?_)
    ¬∑ apply (dist_triangle_left _ _ (x i)).trans
      linarith [hp n i, hp (n + 1) i]
    ¬∑ apply (dist_triangle_left _ _ (x (-i))).trans
      linarith [h'p n i hi , h'p (n + 1) i (by omega)]
  have : CauchySeq p := by
    apply cauchySeq_of_le_geometric (œÅ) (C‚ÇÄ * 2 * Œ¥‚ÇÅ) hT.rho_lt_one (fun n ‚Ü¶ ?_)
    exact (B n).trans_eq (by ring)
  obtain ‚ü®q, hq‚ü© : ‚àÉ q, Tendsto p atTop (ùìù q) := cauchy_iff_exists_le_nhds.mp this
  refine ‚ü®q, ?_, fun n ‚Ü¶ ‚ü®?_, ?_‚ü©‚ü©
  ¬∑ exact IsClosed.mem_of_tendsto hT.isClosed hq (Eventually.of_forall hpA)
  ¬∑ have := hT.continuous
    have : ContinuousAt (T^[n]) q := by fun_prop
    have := Tendsto.dist (tendsto_const_nhds (x := x n)) (Tendsto.comp this hq)
    exact le_of_tendsto' this (fun i ‚Ü¶ hp _ _)
  ¬∑ have := hT.continuous_symm
    have : ContinuousAt (T.symm^[n]) q := by fun_prop
    have := Tendsto.dist (tendsto_const_nhds (x := x (-n))) (Tendsto.comp this hq)
    apply le_of_tendsto this
    filter_upwards [Ici_mem_atTop n] with i hi using h'p _ _ hi

/-- Consider a locally maximal hyperbolic set. Let `Œ¥ > 0`. Let `Œµ` be small enough compared
to `Œ¥`. Then any `Œµ`-pseudo-orbit can be `4Œ¥`-shadowed by a genuine orbit.

We give the conditions on `Œµ` in explicit form, to make it possible to check them uniformly
over families of maps. First, we fix `M` large enough so that `2 * œÅ ^ M * Œ¥ ‚â§ hT.reduceScale Œ¥`.
Then, `Œµ` should be small enough that an `Œµ`-pseudo-orbit does not deviate from a genuine orbit
by more than `hT.reduceScale Œ¥ / 2` until time `M`.
-/
lemma shadowing_precise'
    (hŒ¥ : 0 < Œ¥) (h''Œ¥ : Œ¥ ‚â§ Œ¥‚ÇÇ / 8) (x : ‚Ñ§ ‚Üí X)
    (hx : ‚àÄ n, dist (T (x n)) (x (n + 1)) ‚â§ Œµ) (h'x : ‚àÄ n, x n ‚àà A)
    {M : ‚Ñï} (hM : 2 * C‚ÇÄ * œÅ ^ M * Œ¥ ‚â§ hT.reduceScale Œ¥)
    (hŒµ : ‚àÄ (u : ‚Ñï ‚Üí X), (‚àÄ n, dist (T (u n)) (u (n + 1)) ‚â§ Œµ) ‚Üí
      ‚àÄ i ‚â§ M, dist (T^[i] (u 0)) (u i) ‚â§ hT.reduceScale Œ¥ / 2) :
    ‚àÉ p ‚àà A, ‚àÄ (n : ‚Ñ§), dist (x n) ((T ^ n) p) ‚â§ 4 * Œ¥ := by
  rcases hT.shadowing_precise hŒ¥ h''Œ¥ x hx h'x hM hŒµ with ‚ü®p, hpA, hp‚ü©
  refine ‚ü®p, hpA, fun n ‚Ü¶ ?_‚ü©
  rcases Int.natAbs_eq n with hn | hn <;> set i := n.natAbs <;> rw [hn]
  ¬∑ apply (hp i).1
  ¬∑ convert (hp i).2
    simp only [Equiv.Perm.iterate_eq_pow, zpow_neg, zpow_natCast, DFunLike.coe_fn_eq]
    rfl

end IsExtLocallyMaxHyperbolicSet

/-- Given a positive parameter `Œ¥`, an integer `n` and a uniformly continuous map `f`, one may find
`Œµ > 0` such that any `Œµ`-pseudo-orbit does not deviate from a genuine orbit by more than `Œ¥`
during the first `n` iterates. -/
lemma exists_dist_iterate_le_of_pseudoOrbit
    {Y : Type*} [MetricSpace Y] {f : Y ‚Üí Y} (hf : UniformContinuous f)
    {Œ¥ : ‚Ñù} (hŒ¥ : 0 < Œ¥) (n : ‚Ñï) :
    ‚àÉ Œµ > 0, ‚àÄ (u : ‚Ñï ‚Üí Y), (‚àÄ n, dist (f (u n)) (u (n + 1)) ‚â§ Œµ) ‚Üí
      ‚àÄ i ‚â§ n, dist (f ^[i] (u 0)) (u i) ‚â§ Œ¥ := by
  /- This is a straightforward consequence of uniform continuity (through an induction). -/
  induction n generalizing Œ¥ with
  | zero =>
    refine ‚ü®Œ¥, hŒ¥, fun u hu ‚Ü¶ by simp [hŒ¥.le]‚ü©
  | succ n ih =>
    rcases Metric.uniformContinuous_iff.1 hf (Œ¥ / 2) (by linarith) with ‚ü®Œ¥', Œ¥'pos, h'‚ü©
    rcases ih (show 0 < min Œ¥ (Œ¥' / 2) by positivity) with ‚ü®Œµ, Œµpos, hŒµ‚ü©
    refine ‚ü®min Œµ (Œ¥ / 2), by positivity, fun u hu i hi ‚Ü¶ ?_‚ü©
    rcases Nat.le_succ_iff.1 hi with h'i | rfl
    ¬∑ apply (hŒµ u (fun m ‚Ü¶ ?_) i h'i).trans (min_le_left _ _)
      exact (hu m).trans (min_le_left _ _)
    calc
    dist (f^[n + 1] (u 0)) (u (n + 1))
    _ ‚â§ dist (f (f^[n] (u 0))) (f (u n)) + dist (f (u n)) (u (n + 1)) := by
      rw [iterate_succ_apply']
      apply dist_triangle
    _ ‚â§ Œ¥ / 2 + Œ¥ / 2 := by
      gcongr
      ¬∑ apply (h' _).le
        suffices dist (f^[n] (u 0)) (u n) ‚â§ Œ¥' / 2 by linarith
        exact (hŒµ u (fun m ‚Ü¶ (hu m).trans (min_le_left _ _)) n le_rfl).trans
          (min_le_right _ _)
      ¬∑ exact (hu n).trans (min_le_right _ _)
    _ = Œ¥ := by linarith

namespace IsLocallyMaxHyperbolicSet

/-- Consider a locally maximal hyperbolic set. Let `Œ¥ > 0`. If `Œµ` is small enough, then
any `Œµ`-pseudo-orbit can be `Œ¥`-shadowed by a genuine orbit.

The statement is given here as an existential statement. For explicit sufficient conditions on `Œµ`,
see `shadowing_precise'` (from which this one is derived). -/
theorem shadowing (hT : IsLocallyMaxHyperbolicSet T A) (hŒ¥ : 0 < Œ¥) :
    ‚àÉ Œµ > 0, ‚àÄ (x : ‚Ñ§ ‚Üí X), (‚àÄ n, dist (T (x n)) (x (n + 1)) ‚â§ Œµ) ‚Üí (‚àÄ n, x n ‚àà A) ‚Üí
      ‚àÉ p ‚àà A, ‚àÄ n, dist (x n) ((T ^ n) p) ‚â§ Œ¥ := by
  let hT' := hT.extend
  let Œ¥‚ÇÇ := hT'.reduceScale (hT'.reduceScale hT'.deltaZero)
  let œÅ := hT'.rho
  let Œ¥' := min (Œ¥ / 4) (Œ¥‚ÇÇ / 8)
  have : Œ¥' ‚â§ Œ¥ / 4 := min_le_left _ _
  have Œ¥'_pos : 0 < Œ¥' := by simpa [Œ¥', hŒ¥] using hT'.deltaTwo_pos
  obtain ‚ü®M, hM‚ü© : ‚àÉ M, 2 * hT'.C0 * œÅ ^ M * Œ¥' < hT'.reduceScale Œ¥' := by
    have : Tendsto (fun n ‚Ü¶ 2 * hT'.C0 * œÅ ^ n * Œ¥') atTop (ùìù (2 * hT'.C0 * 0 * Œ¥')) :=
      ((tendsto_pow_atTop_nhds_zero_of_lt_one hT.rho_pos.le hT.rho_lt_one).const_mul _).mul_const _
    rw [mul_zero, zero_mul] at this
    exact ((tendsto_order.1 this).2 _ (hT'.reduceScale_pos Œ¥'_pos)).exists
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ > 0, ‚àÄ (u : ‚Ñï ‚Üí X), (‚àÄ n, dist (T (u n)) (u (n + 1)) ‚â§ Œµ) ‚Üí
      ‚àÄ i ‚â§ M, dist (T^[i] (u 0)) (u i) ‚â§ hT'.reduceScale Œ¥' / 2 := by
    apply exists_dist_iterate_le_of_pseudoOrbit hT.uniformContinuous
    linarith [hT'.reduceScale_pos Œ¥'_pos]
  refine ‚ü®Œµ, Œµpos, fun x hx h'x ‚Ü¶ ?_‚ü©
  rcases hT'.shadowing_precise' Œ¥'_pos (min_le_right _ _) x hx h'x hM.le hŒµ with ‚ü®p, hpA, hp‚ü©
  exact ‚ü®p, hpA, fun n ‚Ü¶ (hp n).trans (by linarith)‚ü©

/-- If an orbit comes back close enough to its starting point, then it is close to a
periodic orbit. -/
theorem closing (hT : IsLocallyMaxHyperbolicSet T A) (hŒ¥ : 0 < Œ¥) :
    ‚àÉ Œµ > 0, ‚àÄ (x : X) (n : ‚Ñï), x ‚àà A ‚Üí dist (T^[n] x) x ‚â§ Œµ ‚Üí
      ‚àÉ y ‚àà A, dist x y ‚â§ Œ¥ ‚àß T^[n] y = y := by
  /- Form a pseudo-orbit `..., x, T x, ..., T^{n-1} x, x, T x, ...`. If `T ^ n x` is close to `x`,
  this is indeed a pseudo-orbit, shadowed by a (unique) genuine orbit. As the pseudo-orbit is
  periodic, the genuine orbit also is, by uniqueness. -/
  rcases hT.expansive with ‚ü®Œ¥‚ÇÅ, Œ¥‚ÇÅ_pos, hŒ¥‚ÇÅ‚ü©
  let Œ¥' := min Œ¥ (Œ¥‚ÇÅ / 2)
  have Œ¥'_pos : 0 < Œ¥' := by positivity
  rcases hT.shadowing Œ¥'_pos with ‚ü®Œµ, Œµ_pos, hŒµ‚ü©
  refine ‚ü®Œµ, Œµ_pos, fun x n hx h'x ‚Ü¶ ?_‚ü©
  rcases eq_or_ne n 0 with rfl | hn
  ¬∑ exact ‚ü®x, hx, by simp [hŒ¥.le], by simp‚ü©
  let u (k : ‚Ñ§) := T^[(k % n).toNat] x
  have D (k : ‚Ñ§) : dist (T (u k)) (u (k + 1)) ‚â§ Œµ := by
    rcases eq_or_ne n 1 with rfl | h'n
    ¬∑ simpa [u] using h'x
    have one_mod : (1 : ‚Ñ§) % n = 1 := Int.emod_eq_of_lt zero_le_one (by omega)
    by_cases h : k % n = n - 1
    ¬∑ have : (k + 1) % n = 0 := by rw [Int.add_emod, h, one_mod]; simp
      simp only [this, Int.pred_toNat, Int.toNat_natCast, h, Int.toNat_zero, ge_iff_le, u,
        ‚Üê iterate_succ_apply' T]
      convert h'x
      omega
    ¬∑ have I : 0 ‚â§ k % n := Int.emod_nonneg k (by omega)
      have : (k + 1) % n = k % n + 1 := by
        rw [Int.add_emod, one_mod]
        apply Int.emod_eq_of_lt (by positivity)
        have : k % n < n := Int.emod_lt_of_pos _ (by omega)
        omega
      simp only [this, Int.toNat_add I zero_le_one, Int.toNat_one, u, iterate_succ_apply',
        dist_self, Œµ_pos.le]
  obtain ‚ü®y, yA, hy‚ü© : ‚àÉ y ‚àà A, ‚àÄ (n : ‚Ñ§), dist (u n) ((T ^ n) y) ‚â§ Œ¥' :=
    hŒµ u D (fun n ‚Ü¶ hT.mapsTo.iterate _ hx)
  refine ‚ü®y, yA, (hy 0).trans (min_le_left _ _), ?_‚ü©
  apply hŒ¥‚ÇÅ _ (hT.mapsTo.iterate _ yA) _ yA (fun k ‚Ü¶ ?_)
  calc dist ((T ^ k) (T^[n] y)) ((T ^ k) y)
  _ = dist ((T ^ (k + n)) y) ((T ^ k) y) := by simp [zpow_add]
  _ ‚â§ dist (u k) ((T ^ (k + n)) y) + dist (u k) ((T ^ k) y) := dist_triangle_left _ _ _
  _ = dist (u (k + n)) ((T ^ (k + n)) y) + dist (u k) ((T ^ k) y) := by simp [u]
  _ ‚â§ Œ¥‚ÇÅ / 2 + Œ¥‚ÇÅ / 2 := by gcongr <;> exact (hy _).trans (min_le_right _ _)
  _ = Œ¥‚ÇÅ := by ring

end IsLocallyMaxHyperbolicSet
