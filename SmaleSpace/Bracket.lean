import Mathlib

/-!
# Ruelle brackets

Convention:

 W^+(o)
  |
  u-----â…s, uâ†
  |       |
  |       |
  |       |
  |       |
--o-------s-- W^-(o)
  |

The stable manifold is the set of points with `â…x, oâ† = x`, or equivalently `â…o, xâ† = o`.
The unstable manifold is the set of points with `â…o, yâ† = y`, or equivalently `â…y, oâ† = o`.
The stable manifold is represented horizontally and the unstable manifold vertically,
so that the notation `â…s, uâ†` for a point parameterized by its stable and unstable components is
analogous to a coordinate notation `(s, u)`. Generally, if `p` and `q` are thought of as
two-dimensional, then `â…p, qâ† = (p.1, q.2)` is the intersection of the vertical line through `p`
and the horizontal line through `q`.
-/

open scoped Uniformity Topology
open Function Set Filter Metric

namespace SmaleSpace

variable (X : Type*) [MetricSpace X] {U V : Set (X Ã— X)} {a b c o s u x y z : X} {Îµ Îµ' Î´ : â„}

/-! ### Spaces with a Ruelle bracket -/

/-- A Ruelle bracket on a space is a bracket operation `â…x, yâ†` corresponding to taking the local
intersection of the stable manifold of `x` and the unstable manifold of `y`. It is only defined for
`x` close enough to `y`.

For the formalization, we require the bracket to be defined everywhere although we will only use
it close to the diagonal, to avoid dependent type issues. We record its algebraic properties,
together with uniform continuity.

We include in the definition the data of a size `Î´â‚€` below which the bracket is well defined.
We could require only its existence, but including it as data makes it possible to be more explicit
in concrete situations. For instance, in subshifts of finite type, we can take `Î´â‚€ = 1`, meaning
that the bracket is well defined on pairs of points sharing the same symbol at coordinate `0`.
-/
class HasRuelleBracket where
  /-- the bracket itself, denoted `â…x, yâ†` once the theory is set up -/
  toFun : X â†’ X â†’ X
  /-- the bracket is only well behaved below some size `Î´â‚€ > 0` -/
  deltaZero : â„
  deltaZero_pos : 0 < deltaZero
  unifCont : UniformContinuousOn (uncurry toFun) {p | dist p.1 p.2 â‰¤ deltaZero}
  refl x : toFun x x = x
  bracket_left' : âˆ€ x y z, dist x y â‰¤ deltaZero â†’ dist y z â‰¤ deltaZero â†’
    toFun (toFun x y) z = toFun x z
  bracket_right' : âˆ€ x y z, dist x y â‰¤ deltaZero â†’ dist y z â‰¤ deltaZero â†’
    toFun x (toFun y z) = toFun x z

instance [h : HasRuelleBracket X] : Bracket X X where
  bracket := h.toFun

export HasRuelleBracket (deltaZero_pos)

attribute [simp] deltaZero_pos

local notation3 "Î´â‚€" => HasRuelleBracket.deltaZero X

variable [HasRuelleBracket X]

lemma uniformContinuousOn_bracket :
    UniformContinuousOn (fun (p : X Ã— X) â†¦ â…p.1, p.2â†) {p : X Ã— X | dist p.1 p.2 â‰¤ Î´â‚€} :=
  HasRuelleBracket.unifCont

lemma continuousOn_bracket :
    ContinuousOn (fun (p : X Ã— X) â†¦ â…p.1, p.2â†) {p : X Ã— X | dist p.1 p.2 â‰¤ Î´â‚€} :=
  (uniformContinuousOn_bracket X).continuousOn

variable {X}

@[simp] lemma bracket_self (x : X) : â…x, xâ† = x :=
  HasRuelleBracket.refl x

lemma bracket_left (h : dist x y â‰¤ Î´â‚€) (h' : dist y z â‰¤ Î´â‚€) :
    â…â…x, yâ†, zâ† = â…x, zâ† :=
  HasRuelleBracket.bracket_left' x y z h h'

lemma bracket_right (h : dist x y â‰¤ Î´â‚€) (h' : dist y z â‰¤ Î´â‚€) :
    â…x, â…y, zâ†â† = â…x, zâ† :=
  HasRuelleBracket.bracket_right' x y z h h'

lemma continuousOn_bracket_left :
    ContinuousOn (fun x â†¦ â…x, oâ†) (closedBall o Î´â‚€) := by
  have : ContinuousOn (fun (x : X) â†¦ (x, o)) (closedBall o Î´â‚€) := by fun_prop
  apply (continuousOn_bracket X).comp this
  simp [MapsTo]

lemma continuousOn_bracket_right :
    ContinuousOn (fun x â†¦ â…o, xâ†) (closedBall o Î´â‚€) := by
  have : ContinuousOn (fun (x : X) â†¦ (o, x)) (closedBall o Î´â‚€) := by fun_prop
  apply (continuousOn_bracket X).comp this
  simp [MapsTo, dist_comm]

/-- If `a` and `b` are close, then `a` and `â…a, bâ†` are close. -/
lemma tendsto_bracket_fst : Tendsto (fun (p : X Ã— X) â†¦ (p.1, â…p.1, p.2â†)) (ğ“¤ X) (ğ“¤ X) := by
  intro V hV
  rcases uniformContinuousOn_bracket X hV with âŸ¨tâ‚, hâ‚, tâ‚‚, hâ‚‚, hV'âŸ©
  rcases entourageProd_subset hâ‚ with âŸ¨u, hu, u', hu', huu'âŸ©
  have : {p : X Ã— X | dist p.1 p.2 < Î´â‚€} âˆˆ ğ“¤ X := Metric.dist_mem_uniformity deltaZero_pos
  have : {p : X Ã— X | dist p.1 p.2 < Î´â‚€} âˆ© u âˆ© u' âˆˆ ğ“¤ X := by grind [Filter.inter_mem]
  apply mem_of_superset this
  rintro âŸ¨a, bâŸ© hab
  have Mâ‚ : ((a, a), (a, b)) âˆˆ tâ‚ := huu' (by simp [entourageProd, mem_uniformity_of_eq hu, hab.2])
  have Mâ‚‚ : ((a, a), (a, b)) âˆˆ tâ‚‚ := by
    simp only [mem_principal] at hâ‚‚
    apply hâ‚‚
    simp only [mem_inter_iff, mem_setOf_eq] at hab
    simp [deltaZero_pos.le, hab.1.1.le]
  have : ((a, a), (a, b)) âˆˆ tâ‚ âˆ© tâ‚‚ := âŸ¨Mâ‚, Mâ‚‚âŸ©
  simpa [â† hV']

/-- If `a` and `b` are close, then `a` and `â…a, bâ†` are close. -/
lemma tendsto_bracket_snd : Tendsto (fun (p : X Ã— X) â†¦ (p.2, â…p.1, p.2â†)) (ğ“¤ X) (ğ“¤ X) :=
  tendsto_id.uniformity_symm.uniformity_trans tendsto_bracket_fst

/-- If three points are close, then the first one is close to the bracket of the other ones.
Version in terms of uniformities. -/
lemma exists_bracket_mem_entourage (hU : U âˆˆ ğ“¤ X) :
    âˆƒ V âˆˆ ğ“¤ X, (âˆ€ x y, (x, y) âˆˆ V â†’ (y, x) âˆˆ V) âˆ§
      âˆ€ x y z, (y, x) âˆˆ V â†’ (x, z) âˆˆ V â†’ ((x, â…y, zâ†) âˆˆ U âˆ§ (â…y, zâ†, x) âˆˆ U) := by
  rcases comp_symm_of_uniformity hU with âŸ¨U', U'_mem, U'_symm, hU'âŸ©
  rcases comp_symm_of_uniformity (tendsto_bracket_fst U'_mem) with âŸ¨V, V_mem, V_symm, hVâŸ©
  refine âŸ¨U' âˆ© V, inter_mem U'_mem V_mem, fun x y hxy â†¦ âŸ¨U'_symm hxy.1, V_symm hxy.2âŸ© ,
    fun x y z hxy hxz â†¦ ?_âŸ©
  have : (y, â…y, zâ†) âˆˆ U' := by
    have : (y, z) âˆˆ V â—‹ V := prodMk_mem_compRel hxy.2 hxz.2
    exact hV this
  exact âŸ¨hU' (prodMk_mem_compRel (U'_symm hxy.1) this),
    hU' (prodMk_mem_compRel (U'_symm this) hxy.1)âŸ©

variable (X) in
/-- If three points are close, then the first one is clsoe to the bracket of the other ones.
Version in terms of distances. -/
lemma exists_dist_bracket_lt (hÎµ : 0 < Îµ) :
    âˆƒ Îµ' âˆˆ Ioc 0 ((min Îµ Î´â‚€) / 2), âˆ€ x y z,
      dist x y â‰¤ Îµ' â†’ dist x z â‰¤ Îµ' â†’ dist (x : X) â…y, zâ† â‰¤ Îµ := by
  have := deltaZero_pos (X := X)
  have : {p : X Ã— X | dist p.1 p.2 < Îµ} âˆˆ ğ“¤ X := Metric.dist_mem_uniformity hÎµ
  rcases exists_bracket_mem_entourage this with âŸ¨V, hV, -, h'VâŸ©
  rcases Metric.mem_uniformity_dist.1 hV with âŸ¨Îµ', Îµ'_pos, hÎµ'âŸ©
  refine âŸ¨min (Îµ' / 2) ((min Îµ Î´â‚€) / 2), âŸ¨by positivity, min_le_right _ _âŸ© , fun x y z hxy hxz â†¦ ?_âŸ©
  refine (h'V _ _ _ (hÎµ' ?_) (hÎµ' ?_)).1.le
  Â· rw [dist_comm]
    exact (hxy.trans (min_le_left _ _)).trans_lt (by linarith)
  Â· exact (hxz.trans (min_le_left _ _)).trans_lt (by linarith)

/-!
### Reducing scales

Given a small scale `Îµ`, we define a smaller scale `reduceScale X Îµ` so that points within the
smaller scale have brackets within distance `Îµ`. We specialize this to `Î´â‚ := reduceScale X Î´â‚€`.
-/

variable (X) in
/-- In a space with a Ruelle bracket, we introduce a function `reduceScale` associating to `Îµ`
a smaller scale so that points within the smaller scale have brackets within distance `Îµ`.
Such a function always exists (by continuity) but instead we provide it as data:
having a control of this function over a whole family of systems is important when proving
structural stability, so we can not just rely on choice to get it.
To get one such arbitrary function, one can use `hasReduceScaleDefault`. -/
class HasReduceScale where
  /-- The scale `reduceScale X Îµ` is small enough compared to `Îµ` so that points within the
  smaller scale have brackets within distance `Îµ`. -/
  reduceScale (Îµ : â„) : â„
  reduceScale_pos {Îµ : â„} (hÎµ : 0 < Îµ) : 0 < reduceScale Îµ
  reduceScale_le_half_self {Îµ : â„} : reduceScale Îµ â‰¤ Îµ / 2
  reduceScale_le_half_deltaZero {Îµ : â„}: reduceScale Îµ â‰¤ Î´â‚€ / 2
  dist_bracket_le_of_le_reduceScale {Îµ : â„} {x y z : X}
    (hxy : dist x y â‰¤ reduceScale Îµ) (hxz : dist x z â‰¤ reduceScale Îµ) :
    dist x â…y, zâ† â‰¤ Îµ

/-- A possible construction of an arbitrary reducing scale function, based on
continuity and choice. Not registered as an instance as one may want to use more explicit
instances in specific situations. -/
noncomputable def hasReduceScaleDefault : HasReduceScale X where
  reduceScale (Îµ : â„) : â„ :=
    if hÎµ : 0 < Îµ then (exists_dist_bracket_lt X hÎµ).choose
    else Îµ
  reduceScale_pos hÎµ := by
    simp only [hÎµ, â†“reduceDIte]
    exact (exists_dist_bracket_lt X hÎµ).choose_spec.1.1
  reduceScale_le_half_self {Îµ} := by
    by_cases hÎµ : 0 < Îµ
    Â· simp only [hÎµ, â†“reduceDIte]
      apply (exists_dist_bracket_lt X hÎµ).choose_spec.1.2.trans
      gcongr
      exact min_le_left _ _
    Â· simp only [hÎµ, â†“reduceDIte]
      linarith
  reduceScale_le_half_deltaZero {Îµ} := by
    by_cases hÎµ : 0 < Îµ
    Â· simp only [hÎµ, â†“reduceDIte]
      apply (exists_dist_bracket_lt X hÎµ).choose_spec.1.2.trans
      gcongr
      exact min_le_right _ _
    Â· simp only [hÎµ, â†“reduceDIte]
      linarith [deltaZero_pos (X := X)]
  dist_bracket_le_of_le_reduceScale {Îµ x y z} hxy hxz := by
    by_cases hÎµ : 0 < Îµ
    Â· simp only [hÎµ, â†“reduceDIte] at hxy hxz
      exact (exists_dist_bracket_lt X hÎµ).choose_spec.2 x y z hxy hxz
    Â· simp only [hÎµ, â†“reduceDIte] at hxy
      have : Îµ = 0 := by linarith [dist_nonneg (x := x) (y := y)]
      simp only [this, dist_le_zero, lt_self_iff_false, â†“reduceDIte] at hxy hxz
      simpa [â† hxy, â† hxz] using this.ge

export HasReduceScale (reduceScale reduceScale_pos reduceScale_le_half_self
  reduceScale_le_half_deltaZero dist_bracket_le_of_le_reduceScale)

section

variable [HasReduceScale X]

lemma reduceScale_le_deltaZero : reduceScale X Îµ â‰¤ Î´â‚€ := by
  linarith [reduceScale_le_half_deltaZero (X := X) (Îµ := Îµ), deltaZero_pos (X := X)]

variable (X) in
/-- A fixed size, sufficiently smaller than `Î´â‚€` to ensure that brackets of points within `Î´â‚`
remain within `Î´â‚€`. -/
noncomputable def deltaOne : â„ := reduceScale X Î´â‚€

local notation3 "Î´â‚" => deltaOne X

@[simp] lemma deltaOne_pos : 0 < Î´â‚ := reduceScale_pos deltaZero_pos

lemma deltaOne_le_half_deltaZero : Î´â‚ â‰¤ Î´â‚€ / 2 := reduceScale_le_half_deltaZero

lemma deltaOne_le_deltaZero : Î´â‚ â‰¤ Î´â‚€ := by
  linarith [deltaOne_le_half_deltaZero (X := X), deltaZero_pos (X := X)]

lemma dist_bracket_le_deltaZero {x y z : X} (hxy : dist x y â‰¤ Î´â‚) (hxz : dist x z â‰¤ Î´â‚) :
    dist x â…y, zâ† â‰¤ Î´â‚€ := dist_bracket_le_of_le_reduceScale hxy hxz

end

/-!
### Reversing stable and unstable directions

It is often convenient to prove something for the stable direction, and then deduce it for the
unstable one, or conversely. For this, we endow the type copy `invDyn X` with the reverse bracket
and the reverse dynamics.
-/

/-- A type copy of a space. The intent is to endow this type copy with the reversed bracket and the
reverse dynamics when `X` itself has a Ruelle bracket and a compatible hyperbolic map. -/
def invDyn (X : Type*) : Type _ := X

/-- The canonical map from the type synonym `invDyn X` from `X`. -/
def ofInvDyn {X : Type*} (x : invDyn X) : X := x

/-- The canonical map from `X` to the type synonym `invDyn X`. -/
def toInvDyn {X : Type*} (x : X) : invDyn X := x

instance : MetricSpace (invDyn X) := inferInstanceAs (MetricSpace X)

/-- We endow the type synonym `invDyn X` with the reversed bracket `â…x, yâ†' := â…y, xâ†`. This
exchanges stable and unstable manifolds, and will be used to deduce results about unstable manifolds
from results about stable manifolds. -/
instance : HasRuelleBracket (invDyn X) where
  toFun x y := toInvDyn â…ofInvDyn y, ofInvDyn xâ†
  deltaZero := Î´â‚€
  deltaZero_pos := deltaZero_pos
  unifCont := by
    have A : UniformContinuousOn (fun (p : X Ã— X) â†¦ Prod.swap p) {p | dist p.1 p.2 â‰¤ Î´â‚€} :=
      uniformContinuous_swap.uniformContinuousOn
    have B : MapsTo (fun (p : X Ã— X) â†¦ p.swap)
      {p | dist p.1 p.2 â‰¤ Î´â‚€} {p | dist p.1 p.2 â‰¤ Î´â‚€} := by simp [MapsTo, dist_comm]
    exact (uniformContinuousOn_bracket X).comp A B
  refl o := by simp [toInvDyn, ofInvDyn]
  bracket_left' x y z hxy hyz := by
    apply bracket_right (X := X)
    Â· rw [dist_comm]
      exact hyz
    Â· rw [dist_comm]
      exact hxy
  bracket_right' x y z hxy hyz := by
    apply bracket_left (X := X)
    Â· rw [dist_comm]
      exact hyz
    Â· rw [dist_comm]
      exact hxy

instance [HasReduceScale X] : HasReduceScale (invDyn X) where
  reduceScale := reduceScale X
  reduceScale_pos := reduceScale_pos (X := X)
  reduceScale_le_half_self := reduceScale_le_half_self (X := X)
  reduceScale_le_half_deltaZero := reduceScale_le_half_deltaZero (X := X)
  dist_bracket_le_of_le_reduceScale hxy hxz :=
    dist_bracket_le_of_le_reduceScale (X := X) hxz hxy


/-!
### Local stable and unstable manifolds, local parametrization with product coordinates
-/

/-- The local stable manifold of `o` within distance `Îµ`, defined as the set of points `s` which
are `Îµ`-close to `o` and satisfy `â…s, oâ† = s`.
Equivalently, these are the points with `â…o, sâ† = o`, see `locStable_eq`.

We use large inequalities in the definition to make sure that local stable manifolds are closed
(and therefore compact in compact spaces). -/
def locStable (Îµ : â„) (o : X) : Set X := {s | dist o s â‰¤ Îµ âˆ§ â…s, oâ† = s}

/-- The local unstable manifold of `o` within distance `Îµ`, defined as the set of points `u`
which are `Îµ`-close to `o` and satisfy `â…o, uâ† = u`.
Equivalently, these are the points with `â…u, oâ† = o`, see `locUnstable_eq`.

We use large inequalities in the definition to make sure that local stable manifolds are closed
(and therefore compact in compact spaces). -/
def locUnstable (Îµ : â„) (o : X) : Set X := {u | dist o u â‰¤ Îµ âˆ§ â…o, uâ† = u}

lemma dist_le_of_mem_locStable (hs : s âˆˆ locStable Îµ o) : dist o s â‰¤ Îµ := hs.1

lemma dist_le_of_mem_locUnstable (hu : u âˆˆ locUnstable Îµ o) : dist o u â‰¤ Îµ := hu.1

lemma bracket_eq_of_mem_locStable (hs : s âˆˆ locStable Îµ o) : â…s, oâ† = s := hs.2

lemma bracket_eq_of_mem_locUnstable (hu : u âˆˆ locUnstable Îµ o) : â…o, uâ† = u := hu.2

lemma locStable_eq (hÎµ : Îµ â‰¤ Î´â‚€) : locStable Îµ o = {s | dist o s â‰¤ Îµ âˆ§ â…o, sâ† = o} := by
  ext s
  have : dist o s = dist s o := dist_comm o s
  simp only [locStable, mem_setOf_eq, and_congr_right_iff]
  intro h
  refine âŸ¨fun h' â†¦ ?_, fun h' â†¦ ?_âŸ©
  Â· rw [â† h', bracket_right, bracket_self] <;> linarith
  Â· rw [â† h', bracket_right, bracket_self] <;> linarith

lemma locUnstable_eq (hÎµ : Îµ â‰¤ Î´â‚€) : locUnstable Îµ o = {u | dist o u â‰¤ Îµ âˆ§ â…u, oâ† = o} :=
  locStable_eq (X := invDyn X) hÎµ

lemma bracket_eq_of_mem_locStable' (hÎµ : Îµ â‰¤ Î´â‚€) (hs : s âˆˆ locStable Îµ o) : â…o, sâ† = o := by
  rw [locStable_eq hÎµ] at hs
  exact hs.2

lemma bracket_eq_of_mem_locUnstable' (hÎµ : Îµ â‰¤ Î´â‚€) (hu : u âˆˆ locUnstable Îµ o) : â…u, oâ† = o :=
  bracket_eq_of_mem_locStable' (X := invDyn X) hÎµ hu

lemma mem_locStable_symm (hÎµ : Îµ â‰¤ Î´â‚€) (hx : x âˆˆ locStable Îµ o) : o âˆˆ locStable Îµ x := by
  rw [locStable_eq hÎµ] at hx
  simpa [locStable, dist_comm] using hx

lemma mem_locUnstable_symm (hÎµ : Îµ â‰¤ Î´â‚€) (hx : x âˆˆ locUnstable Îµ o) : o âˆˆ locUnstable Îµ x :=
  mem_locStable_symm (X := invDyn X) hÎµ hx

lemma mem_locStable_iff_symm (hÎµ : Îµ â‰¤ Î´â‚€) :
    x âˆˆ locStable Îµ o â†” o âˆˆ locStable Îµ x :=
  âŸ¨fun h â†¦ mem_locStable_symm hÎµ h, fun h â†¦ mem_locStable_symm hÎµ hâŸ©

lemma mem_locUnstable_iff_symm (hÎµ : Îµ â‰¤ Î´â‚€) :
    x âˆˆ locUnstable Îµ o â†” o âˆˆ locUnstable Îµ x :=
  mem_locStable_iff_symm (X := invDyn X) hÎµ

lemma bracket_mem_locStable [HasReduceScale X] (hx : dist o x â‰¤ reduceScale X Îµ) :
    â…x, oâ† âˆˆ locStable Îµ o := by
  refine âŸ¨?_, ?_âŸ©
  Â· apply dist_bracket_le_of_le_reduceScale hx
    simp only [dist_self]
    apply le_trans (by positivity) hx
  Â· rw [bracket_left]
    Â· rw [dist_comm]
      apply hx.trans reduceScale_le_deltaZero
    Â· simp [deltaZero_pos.le]

lemma bracket_mem_locUnstable [HasReduceScale X] (hx : dist o x â‰¤ reduceScale X Îµ) :
    â…o, xâ† âˆˆ locUnstable Îµ o :=
  bracket_mem_locStable (X := invDyn X) hx

lemma locStable_mono {Îµ Îµ' : â„} (h : Îµ â‰¤ Îµ') : locStable Îµ o âŠ† locStable Îµ' o := by
  simp only [locStable, setOf_subset_setOf, and_imp]
  grind

lemma locUnstable_mono {Îµ Îµ' : â„} (h : Îµ â‰¤ Îµ') : locUnstable Îµ o âŠ† locUnstable Îµ' o :=
  locStable_mono (X := invDyn X) h

@[simp] lemma locStable_zero : locStable 0 o = {o} := by
  apply Subset.antisymm (fun y hy â†¦ ?_) (fun y hy â†¦ ?_)
  Â· simp only [locStable, dist_le_zero, mem_setOf_eq] at hy
    simp [hy.1]
  Â· simp only [mem_singleton_iff] at hy
    simp [locStable, hy]

@[simp] lemma locUnstable_zero : locUnstable 0 o = {o} :=
  locStable_zero (X := invDyn X)

lemma locStable_eq_empty_of_neg (hÎµ : Îµ < 0) : locStable Îµ o = âˆ… := by
  ext x
  simp [locStable, hÎµ.trans_le (dist_nonneg (x := o) (y := x))]

lemma locUnstable_eq_empty_of_neg (hÎµ : Îµ < 0) : locUnstable Îµ o = âˆ… :=
  locStable_eq_empty_of_neg (X := invDyn X) hÎµ

lemma isClosed_locStable (hÎµ : Îµ â‰¤ Î´â‚€) : IsClosed (locStable Îµ o) := by
  have : ContinuousOn (fun x â†¦ â…o, xâ†) (closedBall o Îµ) :=
    (continuousOn_bracket_right (o := o)).mono (by gcongr)
  convert this.preimage_isClosed_of_isClosed (t := {o}) isClosed_closedBall isClosed_singleton
  ext y
  simp [locStable_eq hÎµ, dist_comm]

lemma isClosed_locUnstable (hÎµ : Îµ â‰¤ Î´â‚€) : IsClosed (locUnstable Îµ o) :=
  isClosed_locStable (X := invDyn X) hÎµ

lemma mem_locStable_trans (hx : x âˆˆ locStable Îµ o) (hy : y âˆˆ locStable Îµ' x)
    (hÎµ : Îµ + Îµ' â‰¤ Î´â‚€) : y âˆˆ locStable (Îµ + Îµ') o := by
  have I : dist o y â‰¤ Îµ + Îµ' := by
    apply (dist_triangle o x y).trans
    linarith [hx.1, hy.1]
  have : 0 â‰¤ Îµ' := le_trans (by positivity) hy.1
  refine âŸ¨I, ?_âŸ©
  rw [â† bracket_eq_of_mem_locStable' ?_ hx, bracket_right]
  Â· exact hy.2
  Â· rw [dist_comm]
    apply I.trans hÎµ
  Â· have := hx.1
    linarith
  Â· linarith

lemma mem_locUnstable_trans (hx : x âˆˆ locUnstable Îµ o) (hy : y âˆˆ locUnstable Îµ' x)
    (hÎµ : Îµ + Îµ' â‰¤ Î´â‚€) : y âˆˆ locUnstable (Îµ + Îµ') o :=
  mem_locStable_trans (X := invDyn X) hx hy hÎµ


variable [HasReduceScale X]
local notation3 "Î´â‚" => deltaOne X

/-- For small enough `Îµ`, one can parametrize a neighborhood of any point `o` by
taking the bracket of points on its stable and unstable manifolds of size `Îµ`.

The fact that the target of this parametrization is indeed a neighborhood of `o` (of
fixed size `reduceScale X Îµ`) is given in `ball_reduceScale_subset_target_localProduct`.
-/
@[simps!]
def localProductEquiv (hÎµ : Îµ â‰¤ Î´â‚) (o : X) : PartialEquiv (X Ã— X) X where
  toFun p := â…p.1, p.2â†
  invFun z := (â…z, oâ†, â…o, zâ†)
  source := (locStable Îµ o) Ã—Ë¢ (locUnstable Îµ o)
  target := {y | dist o y â‰¤ Î´â‚€ âˆ§ dist o â…o, yâ† â‰¤ Îµ âˆ§ dist o â…y, oâ† â‰¤ Îµ}
  map_source' := by
    rintro âŸ¨s, uâŸ© âŸ¨hs, huâŸ©
    have h's : dist o s â‰¤ Îµ := dist_le_of_mem_locStable hs
    have h'u : dist o u â‰¤ Îµ := dist_le_of_mem_locUnstable hu
    have : dist s u â‰¤ Î´â‚€ := by
      linarith [dist_triangle_left s u o, deltaOne_le_half_deltaZero (X := X)]
    have := deltaOne_le_deltaZero (X := X)
    simp only [mem_setOf_eq]
    refine âŸ¨?_, ?_, ?_âŸ©
    Â· exact dist_bracket_le_deltaZero (by linarith) (by linarith)
    Â· rwa [bracket_right, bracket_eq_of_mem_locUnstable hu] <;> linarith
    Â· rwa [bracket_left, bracket_eq_of_mem_locStable hs]
      Â· linarith
      Â· rw [dist_comm]
        linarith
  map_target' := by
    rintro x âŸ¨hx_main, hx, h'xâŸ©
    simp only [locStable, locUnstable, mem_prod, mem_setOf_eq, h'x, true_and, hx]
    rw [bracket_left, bracket_right] <;> simp [deltaZero_pos.le, dist_comm, hx_main]
  left_inv' := by
    rintro âŸ¨s, uâŸ© âŸ¨hs, huâŸ©
    have h's : dist o s â‰¤ Îµ := dist_le_of_mem_locStable hs
    have h'u : dist o u â‰¤ Îµ := dist_le_of_mem_locUnstable hu
    have : dist s u â‰¤ Î´â‚€ := by
      linarith [dist_triangle_left s u o, deltaOne_le_half_deltaZero (X := X)]
    have := deltaOne_le_deltaZero (X := X)
    simp only [Prod.mk.injEq]
    constructor
    Â· rw [bracket_left]
      Â· exact bracket_eq_of_mem_locStable hs
      Â· linarith
      Â· rw [dist_comm]
        linarith
    Â· rw [bracket_right]
      Â· exact bracket_eq_of_mem_locUnstable hu
      Â· linarith
      Â· linarith
  right_inv' := by
    intro x âŸ¨hx, h'x, h''xâŸ©
    simp only
    rw [bracket_left, bracket_right, bracket_self]
    Â· rwa [dist_comm]
    Â· exact hx
    Â· rwa [dist_comm]
    Â· linarith [deltaOne_le_deltaZero (X := X)]

lemma continuousOn_localProductEquiv (hÎµ : Îµ â‰¤ Î´â‚) :
    ContinuousOn (localProductEquiv hÎµ o) (localProductEquiv hÎµ o).source := by
  apply (continuousOn_bracket X).mono
  rintro âŸ¨s, uâŸ© âŸ¨âŸ¨hs, h'sâŸ©, âŸ¨hu, h'uâŸ©âŸ©
  simp only [mem_setOf_eq] at hs hu âŠ¢
  linarith [dist_triangle_left s u o, deltaOne_le_half_deltaZero (X := X)]

lemma continuousOn_symm_localProductEquiv (hÎµ : Îµ â‰¤ Î´â‚) :
    ContinuousOn (localProductEquiv hÎµ o).symm (localProductEquiv hÎµ o).target := by
  apply ContinuousOn.prodMk
  Â· apply (continuousOn_bracket X).comp (Continuous.prodMk_left o).continuousOn
    intro x âŸ¨hxo, hx, h'xâŸ©
    simpa [dist_comm] using hxo
  Â· apply (continuousOn_bracket X).comp (Continuous.prodMk_right o).continuousOn
    intro x âŸ¨hxo, hx, h'xâŸ©
    exact hxo

/-- Given a small enough `Îµ`, the ball around `o` for the smaller
scale `reduceScale X Îµ` is covered by the local product parametrization of size `Îµ`. -/
lemma closedBall_reduceScale_subset_target_localProductEquiv (hÎµ : Îµ â‰¤ Î´â‚) :
    closedBall o (reduceScale X Îµ) âŠ† (localProductEquiv hÎµ o).target := by
  rcases lt_trichotomy Îµ 0 with hÎµ | rfl | hÎµ
  Â· rw [Metric.closedBall_eq_empty.2]
    Â· simp
    apply reduceScale_le_half_self.trans_lt
    linarith
  Â· intro y hy
    have : reduceScale X 0 â‰¤ 0 := reduceScale_le_half_self.trans_eq (by simp)
    have : y = o := by simpa using (mem_closedBall.1 hy).trans this
    simp [this, deltaZero_pos.le]
  intro y (hy : dist y o â‰¤ reduceScale X Îµ)
  rw [dist_comm] at hy
  simp only [localProductEquiv_target, mem_setOf_eq]
  refine âŸ¨?_, ?_, ?_âŸ©
  Â· exact hy.trans reduceScale_le_deltaZero
  Â· apply dist_bracket_le_of_le_reduceScale _ hy
    simp [(reduceScale_pos hÎµ).le]
  Â· apply dist_bracket_le_of_le_reduceScale hy
    simp [(reduceScale_pos hÎµ).le]

lemma target_localProductEquiv_mem_nhds (hÎµ : Îµ â‰¤ Î´â‚) (h'Îµ : 0 < Îµ) :
    (localProductEquiv hÎµ o).target âˆˆ ğ“ o := by
  apply mem_of_superset _ (closedBall_reduceScale_subset_target_localProductEquiv hÎµ)
  exact closedBall_mem_nhds _ (reduceScale_pos h'Îµ)

end SmaleSpace
