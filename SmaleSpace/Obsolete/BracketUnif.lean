import Mathlib

/-!
# Ruelle brackets

Convention:

 W^+(o)
  |
  u-----â…s, uâ†
  |       |
  |       |
  |       |
  |       |
--o-------s-- W^-(o)
  |

The stable manifold is the set of points with `â…x, oâ† = x`, or equivalently `â…o, xâ† = o`.
The unstable manifold is the set of points with `â…o, yâ† = y`, or equivalently `â…y, oâ† = o`.
The stable manifold is represented horizontally and the unstable manifold vertically,
so that the notation `â…s, uâ†` for a point parameterized by its stable and unstable components is
analogous to a coordinate notation `(s, u)`. Generally, if `p` and `q` are thought of as
two-dimensional, then `â…p, qâ† = (p.1, q.2)` is the intersection of the vertical line through `p`
and the horizontal line through `q`.

This file is an experiment to implement Ruelle bracket in terms of uniformities. This works quite
well, but for subsequent results this is not enough (convergence of geometric series is key for
the shadowing lemma, and not really natural to express with uniformities). So we've switched
to a design based on distances, and only keep this file for the record.
-/

open scoped Uniformity Topology
open Function Set Filter SetRel

namespace SmaleSpaceUniformityExperiment

variable (X : Type*) [UniformSpace X] {U V : Set (X Ã— X)} {a b c o s u x y z : X}

/-! ### Spaces with a Ruelle bracket -/

/-- A Ruelle bracket on a space is a bracket operation `â…x, yâ†` corresponding to taking the local
intersection of the stable manifold of `x` and the unstable manifold of `y`. It is only defined for
`x` close enough to `y`.

For the formalization, we require the bracket to be defined everywhere although we will only use
it close to the diagonal, to avoid dependent type issues. We record its algebraic properties,
together with uniform continuity.

We include in the definition the data of an entourage where the bracket is well-defined. We could
require only its existence, but including it as data makes it possible to be more explicit in
concrete situations. For instance, in subshifts of finite type, we can take for `mainEnt` the
set of pairs of points sharing the same symbol at coordinate `0`.
-/
class HasRuelleBracket where
  /-- the bracket itself, denoted `â…x, yâ†` once the theory is set up -/
  toFun : X â†’ X â†’ X
  /-- An entourage on which the bracket is uniformly continuous and involutive -/
  mainEnt : Set (X Ã— X)
  mainEnt_mem : mainEnt âˆˆ ğ“¤ X
  mainEnt_symm {x y : X} (h : (x, y) âˆˆ mainEnt) : (y, x) âˆˆ mainEnt
  unifCont : UniformContinuousOn (uncurry toFun) mainEnt
  refl x : toFun x x = x
  bracket_left' : âˆ€ x y z, (x, y) âˆˆ mainEnt â†’ (y, z) âˆˆ mainEnt â†’
    toFun (toFun x y) z = toFun x z
  bracket_right' : âˆ€ x y z, (x, y) âˆˆ mainEnt â†’ (y, z) âˆˆ mainEnt â†’
    toFun x (toFun y z) = toFun x z

instance [h : HasRuelleBracket X] : Bracket X X where
  bracket := h.toFun

export HasRuelleBracket (mainEnt mainEnt_mem mainEnt_symm)

variable [HasRuelleBracket X]

lemma uniformContinuousOn_bracket : UniformContinuousOn (fun (p : X Ã— X) â†¦ â…p.1, p.2â†) mainEnt :=
  HasRuelleBracket.unifCont

lemma continuousOn_bracket : ContinuousOn (fun (p : X Ã— X) â†¦ â…p.1, p.2â†) mainEnt :=
  (uniformContinuousOn_bracket X).continuousOn

variable {X}

@[simp] lemma bracket_self (x : X) : â…x, xâ† = x :=
  HasRuelleBracket.refl x

@[simp] lemma prodMk_self_mem_mainEnt (x : X) : (x, x) âˆˆ mainEnt :=
  mem_uniformity_of_eq mainEnt_mem rfl

lemma bracket_left (h : (x, y) âˆˆ mainEnt) (h' : (y, z) âˆˆ mainEnt) :
    â…â…x, yâ†, zâ† = â…x, zâ† :=
  HasRuelleBracket.bracket_left' x y z h h'

lemma bracket_right (h : (x, y) âˆˆ mainEnt) (h' : (y, z) âˆˆ mainEnt) :
    â…x, â…y, zâ†â† = â…x, zâ† :=
  HasRuelleBracket.bracket_right' x y z h h'

/-- If `a` and `b` are close, then `a` and `â…a, bâ†` are close. -/
lemma tendsto_bracket_fst : Tendsto (fun (p : X Ã— X) â†¦ (p.1, â…p.1, p.2â†)) (ğ“¤ X) (ğ“¤ X) := by
  intro V hV
  rcases uniformContinuousOn_bracket X hV with âŸ¨tâ‚, hâ‚, tâ‚‚, hâ‚‚, hV'âŸ©
  rcases entourageProd_subset hâ‚ with âŸ¨u, hu, u', hu', huu'âŸ©
  have : mainEnt âˆ© u âˆ© u' âˆˆ ğ“¤ X := by grind [Filter.inter_mem, mainEnt_mem]
  apply mem_of_superset this
  rintro âŸ¨a, bâŸ© hab
  have Mâ‚ : ((a, a), (a, b)) âˆˆ tâ‚ := huu' (by simp [entourageProd, mem_uniformity_of_eq hu, hab.2])
  have Mâ‚‚ : ((a, a), (a, b)) âˆˆ tâ‚‚ := by
    simp only [mem_principal] at hâ‚‚
    apply hâ‚‚
    simp [mem_uniformity_of_eq mainEnt_mem, hab.1.1]
  have : ((a, a), (a, b)) âˆˆ tâ‚ âˆ© tâ‚‚ := âŸ¨Mâ‚, Mâ‚‚âŸ©
  simpa [â† hV']

/-- If `a` and `b` are close, then `a` and `â…a, bâ†` are close. -/
lemma tendsto_bracket_snd : Tendsto (fun (p : X Ã— X) â†¦ (p.2, â…p.1, p.2â†)) (ğ“¤ X) (ğ“¤ X) :=
  tendsto_id.uniformity_symm.uniformity_trans tendsto_bracket_fst

/-- If three points are close, then the first one is close to the bracket of the other ones. -/
lemma exists_bracket_mem (hU : U âˆˆ ğ“¤ X) :
    âˆƒ V âˆˆ ğ“¤ X, (âˆ€ x y, (x, y) âˆˆ V â†’ (y, x) âˆˆ V) âˆ§
      âˆ€ x y z, (y, x) âˆˆ V â†’ (x, z) âˆˆ V â†’ ((x, â…y, zâ†) âˆˆ U âˆ§ (â…y, zâ†, x) âˆˆ U) := by
  rcases comp_symm_of_uniformity hU with âŸ¨U', U'_mem, U'_symm, hU'âŸ©
  rcases comp_symm_of_uniformity (tendsto_bracket_fst U'_mem) with âŸ¨V, V_mem, V_symm, hVâŸ©
  refine âŸ¨U' âˆ© V, inter_mem U'_mem V_mem, fun x y hxy â†¦ âŸ¨U'_symm hxy.1, V_symm hxy.2âŸ© ,
    fun x y z hxy hxz â†¦ ?_âŸ©
  have : (y, â…y, zâ†) âˆˆ U' := by
    have : (y, z) âˆˆ V â—‹ V := prodMk_mem_comp hxy.2 hxz.2
    exact hV this
  exact âŸ¨hU' (prodMk_mem_comp (U'_symm hxy.1) this),
    hU' (prodMk_mem_comp (U'_symm this) hxy.1)âŸ©

/-!
### Reducing entourages

Given an entourage `U`, we construct a smaller entourage `bracketRoot U` such that composing points
in `bracketRoot U` or taking their brackets, one remains in `U`.
-/

open scoped Classical in
/-- Given an entourage `U`, the entourage `bracketRoot U` is smaller, designed so that the Ruelle
bracket of points inside it remain in `U`. Iterate this construction if you need stability
of longer brackets. -/
def bracketRoot (U : Set (X Ã— X)) : Set (X Ã— X) :=
  if hU : U âˆˆ ğ“¤ X then
    (comp_symm_of_uniformity (exists_bracket_mem hU).choose_spec.1).choose
  else âˆ…

lemma bracketRoot_symm (h : (a, b) âˆˆ bracketRoot U) : (b, a) âˆˆ bracketRoot U := by
  by_cases hU : U âˆˆ ğ“¤ X; swap
  Â· simp [bracketRoot, hU] at h
  simp only [bracketRoot, hU, â†“reduceDIte] at h âŠ¢
  exact (comp_symm_of_uniformity (exists_bracket_mem hU).choose_spec.1).choose_spec.2.1 h

lemma bracketRoot_mem_unif (hU : U âˆˆ ğ“¤ X) : bracketRoot U âˆˆ ğ“¤ X := by
  simp only [bracketRoot, hU, â†“reduceDIte]
  exact (comp_symm_of_uniformity (exists_bracket_mem hU).choose_spec.1).choose_spec.1

lemma bracket_right_mem_of_mem_bracketRoot
    (h : (a, b) âˆˆ bracketRoot U) (h' : (b, c) âˆˆ bracketRoot U) : (b, â…a, câ†) âˆˆ U := by
  by_cases hU : U âˆˆ ğ“¤ X; swap
  Â· simp [bracketRoot, hU] at h
  let U' := (exists_bracket_mem hU).choose
  have I : bracketRoot U âŠ† U' := by
    have : bracketRoot U â—‹ bracketRoot U âŠ† U' := by
      simp only [bracketRoot, hU, â†“reduceDIte]
      exact (comp_symm_of_uniformity (exists_bracket_mem hU).choose_spec.1).choose_spec.2.2
    exact (subset_comp_self_of_mem_uniformity (bracketRoot_mem_unif hU)).trans this
  exact ((exists_bracket_mem hU).choose_spec.2.2 _ _ _ (I h) (I h')).1

lemma bracket_left_mem_of_mem_bracketRoot
    (h : (a, b) âˆˆ bracketRoot U) (h' : (b, c) âˆˆ bracketRoot U) : (â…a, câ†, b) âˆˆ U := by
  by_cases hU : U âˆˆ ğ“¤ X; swap
  Â· simp [bracketRoot, hU] at h
  let U' := (exists_bracket_mem hU).choose
  have I : bracketRoot U âŠ† U' := by
    have : bracketRoot U â—‹ bracketRoot U âŠ† U' := by
      simp only [bracketRoot, hU, â†“reduceDIte]
      exact (comp_symm_of_uniformity (exists_bracket_mem hU).choose_spec.1).choose_spec.2.2
    exact (subset_comp_self_of_mem_uniformity (bracketRoot_mem_unif hU)).trans this
  exact ((exists_bracket_mem hU).choose_spec.2.2 _ _ _ (I h) (I h')).2

lemma bracketRoot_subset_self : bracketRoot U âŠ† U := by
  rintro âŸ¨a, bâŸ© hab
  simpa using bracket_right_mem_of_mem_bracketRoot (bracketRoot_symm hab) hab

lemma comp_mem_of_mem_bracketRoot
    (h : (a, b) âˆˆ bracketRoot U) (h' : (b, c) âˆˆ bracketRoot U) : (a, c) âˆˆ U := by
  by_cases hU : U âˆˆ ğ“¤ X; swap
  Â· simp [bracketRoot, hU] at h
  let U' := (exists_bracket_mem hU).choose
  have hU'U : U' âŠ† U := by
    rintro âŸ¨x, yâŸ© hxy
    have hyx : (y, x) âˆˆ U' := (exists_bracket_mem hU).choose_spec.2.1 _ _ hxy
    simpa using ((exists_bracket_mem hU).choose_spec.2.2 _ _ _ hyx hxy).1
  simp only [bracketRoot, hU, â†“reduceDIte] at h h'
  have := (comp_symm_of_uniformity (exists_bracket_mem hU).choose_spec.1).choose_spec.2.2
  apply this.trans hU'U (prodMk_mem_comp h h')

/-!
### Small enough entourages

Stable and unstable manifolds are only local objects. We parametrize them using an entourage `U`.
These constructions only work well if `U` is small enough. We record in this section smallness
conditions that are suitable for the task. They are explicit enough that, in the case of subshifts
of finite type, pairs of points with matching zeroth coordinate form a small enough entourage --
which is important since in this specific example we really want to have explicit and rather large
local stable and unstable manifolds.
-/

/-- An entourage is *small enough* it it is contained in the main entourage where the bracket is
well behaved, and the bracket of points also remains in the main entourage. -/
structure SmallEnough (U : Set (X Ã— X)) : Prop where
  subset_mainEnt : U âŠ† mainEnt
  comp_subset_mainEnt : U â—‹ U âŠ† mainEnt
  bracket_mem {x y z : X} (h : (y, x) âˆˆ U) (h' : (x, z) âˆˆ U) : (x, â…y, zâ†) âˆˆ mainEnt

lemma SmallEnough.mono (hU : SmallEnough U) (h : V âŠ† U) : SmallEnough V where
  subset_mainEnt := h.trans hU.subset_mainEnt
  comp_subset_mainEnt := (comp_subset_comp h h).trans hU.comp_subset_mainEnt
  bracket_mem hyx hxz := hU.bracket_mem (h hyx) (h hxz)

/-- Every set contained in a small entourage is small enough. -/
lemma eventually_smallEnough : âˆ€á¶  U in (ğ“¤ X).smallSets, SmallEnough U := by
  rw [eventually_smallSets]
  rcases comp_symm_of_uniformity (mainEnt_mem (X := X)) with âŸ¨U, U_mem, U_symm, hUâŸ©
  rcases exists_bracket_mem mainEnt_mem (X := X) with âŸ¨V, V_mem, V_symm, hVâŸ©
  refine âŸ¨U âˆ© V, inter_mem U_mem V_mem, fun t ht â†¦ âŸ¨by grind [subset_comp_self_of_mem_uniformity],
    ?_, by grindâŸ©âŸ©
  have : t âŠ† U := by grind
  exact (comp_subset_comp this this).trans hU

/-!
### Local stable and unstable manifolds, local parametrization with product coordinates
-/

/-- The local stable manifold of `o` inside an entourage `U`, defined as the set of points `s` which
are `U`-close to `o` and satisfy `â…s, oâ† = s`.
Equivalently, these are the points with `â…o, sâ† = o`, see `locStable_eq`. -/
def locStable (U : Set (X Ã— X)) (o : X) : Set X := {s | (s, o) âˆˆ U âˆ§ â…s, oâ† = s}

/-- The local unstable manifold of `o` inside an entourage `U`, defined as the set of points `u`
which are `U`-close to `o` and satisfy `â…o, uâ† = u`.
Equivalently, these are the points with `â…u, oâ† = o`, see `locUnstable_eq`. -/
def locUnstable (U : Set (X Ã— X)) (o : X) : Set X := {u | (o, u) âˆˆ U âˆ§ â…o, uâ† = u}

lemma mem_of_mem_locStable (hs : s âˆˆ locStable U o) : (s, o) âˆˆ U := hs.1

lemma bracket_eq_of_mem_locStable (hs : s âˆˆ locStable U o) : â…s, oâ† = s := hs.2

lemma locStable_eq (hU : SmallEnough U) : locStable U o = {s | (s, o) âˆˆ U âˆ§ â…o, sâ† = o} := by
  ext s
  simp only [locStable, mem_setOf_eq, and_congr_right_iff]
  intro h
  refine âŸ¨fun h' â†¦ ?_, fun h' â†¦ ?_âŸ©
  Â· rw [â† h', bracket_right, bracket_self]
    Â· exact mainEnt_symm (hU.subset_mainEnt h)
    Â· exact hU.subset_mainEnt h
  Â· rw [â† h', bracket_right, bracket_self]
    Â· exact hU.subset_mainEnt h
    Â· exact mainEnt_symm (hU.subset_mainEnt h)

lemma mem_of_mem_locUnstable (hu : u âˆˆ locUnstable U o) : (o, u) âˆˆ U := hu.1

lemma bracket_eq_of_mem_locUnstable (hu : u âˆˆ locUnstable U o) : â…o, uâ† = u := hu.2

lemma locUnstable_eq (hU : SmallEnough U) : locUnstable U o = {u | (o, u) âˆˆ U âˆ§ â…u, oâ† = o} := by
  ext u
  simp only [locUnstable, mem_setOf_eq, and_congr_right_iff]
  intro h
  refine âŸ¨fun h' â†¦ ?_, fun h' â†¦ ?_âŸ©
  Â· rw [â† h', bracket_left, bracket_self]
    Â· exact hU.subset_mainEnt h
    Â· exact mainEnt_symm (hU.subset_mainEnt h)
  Â· rw [â† h', bracket_left, bracket_self]
    Â· exact mainEnt_symm (hU.subset_mainEnt h)
    Â· exact hU.subset_mainEnt h

/-- If an entourage is small enough, then one can parametrize a neighborhood of any point `o` by
taking the bracket of points on its stable and unstable manifolds.

The fact that the target of this parametrization is indeed a neighborhood of `o` (of fixed size)
is given in `SmallEnough.bracketRoot_subset_target_localProduct`.
-/
@[simps!]
def SmallEnough.localProductEquiv (hU : SmallEnough U) (o : X) : PartialEquiv (X Ã— X) X where
  toFun p := â…p.1, p.2â†
  invFun z := (â…z, oâ†, â…o, zâ†)
  source := (locStable U o) Ã—Ë¢ (locUnstable U o)
  target := {y | (o, y) âˆˆ mainEnt âˆ§ (o, â…o, yâ†) âˆˆ U âˆ§ (â…y, oâ†, o) âˆˆ U}
  map_source' := by
    rintro âŸ¨s, uâŸ© âŸ¨hs, huâŸ©
    have h's : (s, o) âˆˆ U := mem_of_mem_locStable hs
    have h'u : (o, u) âˆˆ U := mem_of_mem_locUnstable hu
    simp only [mem_setOf_eq]
    refine âŸ¨?_, ?_, ?_âŸ©
    Â· apply hU.bracket_mem h's h'u
    Â· rwa [bracket_right, bracket_eq_of_mem_locUnstable hu]
      Â· apply mainEnt_symm
        exact hU.subset_mainEnt h's
      Â· exact hU.comp_subset_mainEnt (prodMk_mem_comp h's h'u)
    Â· rwa [bracket_left, bracket_eq_of_mem_locStable hs]
      Â· exact hU.comp_subset_mainEnt (prodMk_mem_comp h's h'u)
      Â· apply mainEnt_symm
        exact hU.subset_mainEnt h'u
  map_target' := by
    rintro x âŸ¨hx_main, hx, h'xâŸ©
    simp [locStable, locUnstable, bracket_left, bracket_right, hx_main, mainEnt_symm hx_main,
      hx, h'x]
  left_inv' := by
    rintro âŸ¨s, uâŸ© âŸ¨hs, huâŸ©
    have h's : (s, o) âˆˆ U := mem_of_mem_locStable hs
    have h'u : (o, u) âˆˆ U := mem_of_mem_locUnstable hu
    simp only [Prod.mk.injEq]
    constructor
    Â· rw [bracket_left]
      Â· exact bracket_eq_of_mem_locStable hs
      Â· apply hU.comp_subset_mainEnt (prodMk_mem_comp h's h'u)
      Â· apply mainEnt_symm
        apply hU.subset_mainEnt h'u
    Â· rw [bracket_right]
      Â· exact bracket_eq_of_mem_locUnstable hu
      Â· apply mainEnt_symm
        apply hU.subset_mainEnt h's
      Â· apply hU.comp_subset_mainEnt (prodMk_mem_comp h's h'u)
  right_inv' := by
    intro x âŸ¨hx, h'x, h''xâŸ©
    simp only
    rw [bracket_left, bracket_right, bracket_self]
    Â· exact mainEnt_symm hx
    Â· exact hx
    Â· exact mainEnt_symm hx
    Â· exact hU.subset_mainEnt h'x

lemma SmallEnough.continuousOn_localProductEquiv (hU : SmallEnough U) :
    ContinuousOn (hU.localProductEquiv o) (hU.localProductEquiv o).source := by
  apply (continuousOn_bracket X).mono
  rintro âŸ¨s, uâŸ© âŸ¨âŸ¨hs, h'sâŸ©, âŸ¨hu, h'uâŸ©âŸ©
  exact hU.comp_subset_mainEnt (prodMk_mem_comp hs hu)

lemma SmallEnough.continuousOn_symm_localProductEquiv (hU : SmallEnough U) :
    ContinuousOn (hU.localProductEquiv o).symm (hU.localProductEquiv o).target := by
  change ContinuousOn (fun z â†¦ (â…z, oâ†, â…o, zâ†))
    {y | (o, y) âˆˆ mainEnt âˆ§ (o, â…o, yâ†) âˆˆ U âˆ§ (â…y, oâ†, o) âˆˆ U}
  apply ContinuousOn.prodMk
  Â· apply (continuousOn_bracket X).comp (Continuous.prodMk_left o).continuousOn
    intro x âŸ¨hxo, hx, h'xâŸ©
    exact mainEnt_symm hxo
  Â· apply (continuousOn_bracket X).comp (Continuous.prodMk_right o).continuousOn
    intro x âŸ¨hxo, hx, h'xâŸ©
    exact hxo

/-- Given a small enough entourage `U`, the ball around `o` for the smaller
entourage `bracketRoot U` is covered by the local product parametrization coming from `U`. -/
lemma SmallEnough.bracketRoot_subset_target_localProductEquiv (hU : SmallEnough U) :
    UniformSpace.ball o (bracketRoot U) âŠ† (hU.localProductEquiv o).target := by
  by_cases h'U : U âˆˆ ğ“¤ X; swap
  Â· simp [bracketRoot, h'U, UniformSpace.ball]
  intro y (hy : (o, y) âˆˆ bracketRoot U)
  simp only [localProductEquiv_target, mem_setOf_eq]
  have hoo : (o, o) âˆˆ bracketRoot U := mem_uniformity_of_eq (bracketRoot_mem_unif h'U) rfl
  refine âŸ¨?_, ?_, ?_âŸ©
  Â· apply hU.subset_mainEnt
    exact bracketRoot_subset_self hy
  Â· exact bracket_right_mem_of_mem_bracketRoot hoo hy
  Â· exact bracket_left_mem_of_mem_bracketRoot (bracketRoot_symm hy) hoo

lemma SmallEnough.target_localProductEquiv_mem_nhds (hU : SmallEnough U) (h : U âˆˆ ğ“¤ X) :
    (hU.localProductEquiv o).target âˆˆ ğ“ o := by
  apply Filter.mem_of_superset _ hU.bracketRoot_subset_target_localProductEquiv
  exact UniformSpace.ball_mem_nhds o (bracketRoot_mem_unif h)

end SmaleSpaceUniformityExperiment
